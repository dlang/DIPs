# @mustUse

| Field           | Value                                                           |
|-----------------|-----------------------------------------------------------------|
| DIP:            | 1038                                                            |
| Review Count:   | 2                                                               |
| Author:         | Paul Backus (snarwin@gmail.com)                                 |
| Implementation: | <https://github.com/dlang/dmd/pull/11765>                       |
| Status:         | Formal Assessment                                               |

## Abstract

This DIP proposes a new attribute, `@mustUse`, that allows the programmer to
make ignoring a function's return value into a compile-time error. It can be
used to implement alternative error-handling mechanisms for code that cannot
use exceptions, and to prevent bugs when interfacing with external functions
that report errors via their return values.

## Contents
* [Rationale](#rationale)
* [Prior Work](#prior-work)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Reference](#reference)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale

### Error handling without exceptions

Currently in D, the only generally applicable way for a function to send a
signal to its caller that the caller cannot ignore is to throw an exception.
For a variety of reasons, however, the use of exceptions is not always possible
or desirable. Examples of code that may want or need to avoid exceptions
include:

* code that is written in a language other than D (for example, C or C++);
* code written in D that may be called from another language;
* code that does not want to depend on the D runtime;
* code that cannot afford the run-time performance overhead of exceptions.

Since D is intended to be a systems language suitable for writing low-level,
high-performance code with seamless C and C++ interoperability, its feature set
should support reliable error handling in all of these use-cases.

`@mustUse` helps D achieve this goal by enabling reliable error handling both
for functions that use raw error codes (most commonly, `extern(C)` functions)
and for functions that use algebraic "result" types to signal failure to their
callers.

#### Alternatives

One possible alternative to exceptions, [proposed by Vladimir
Panteleev][SuccessType], is for a function to return an error code wrapped in a
`struct` that `assert`s (or `throw`s) in its destructor at run time if it has
not been used (where "using" means calling a particular method). While this
addresses some of the use-cases above, it has several shortcomings compared to
`@mustUse`:

* it reports ignored errors at run time rather than compile time;
* it cannot be used directly with functions written in other languages;
* it requires additional syntax in both the callee and its callers to wrap and unwrap the error code.

By contrast, `@mustUse` can be used without caveats to provide compile-time
protection against ignored errors in all of the use-cases listed above.

Another alternative is for a function to return error information via an `out`
parameter. Since a call to the function will not compile with a missing
argument, the calling code is forced at compile time to visibly acknowledge the
possibility of an error.

Unfortunately, using `out` parameters for error handling is not a
generally applicable solution because the programmer is not always free to
change a function's signature to include an `out` parameter. Reasons for this
include:

* the function's signature is part of an established public API, and changing
  it would break other code;
* the function is used as a callback by another function that requires it to
  have a specific signature;
* the function is an operator overload.

By contrast, `@mustUse` can be used with any function because all functions
have a return type.

### Functions without specified side effects

Some functions have side effects but are nevertheless unlikely to be called for
those side effects alone. Examples of such functions include:

* functions that acquire resources, such as `malloc` and `mmap`;
* functions that generate random numbers, such as `rand` and `uniform`;
* generic functions that may or may not cause side effects depending on their
  arguments, such as `filter` and `map`.

What these functions have in common is that their side effects, if any, are
considered implementation details, rather than being part of their documented
behavior. As a result, calling code cannot rely on them to cause any *specific*
side effects without risking breakage if and when those implementation details
change.

Though ignoring the return values of these functions is unlikely to result in
disaster, it is still a probable programming mistake which `@mustUse` could
help guard against.

This DIP does not recommend adding `@mustUse` to any existing functions in
Phobos or the D runtime since doing so would constitute a breaking API change.
However, authors of new code would still benefit from having `@mustUse` in
the language and existing projects (including Phobos and the D runtime) could
adopt `@mustUse` on a case-by-case basis if the benefit were judged to be
worth the potential for breakage.

## Prior Work

### In D

The D compiler already warns about discarding the value of an expression if it
has no side effects, including the case where the expression is a call to a
strongly `pure` and `nothrow` function. An attribute that would allow the
programmer to extend this warning by marking specific functions or types as
non-discardable has been proposed several times on the D issue tracker and
forums; see the [Reference](#reference) section below for details.

### In Other Languages

* C++17's [`[[nodiscard]]` attribute][Cpp17Nodiscard].
* Rust's [`#[must_use]` attribute][RustMustUse].
* GCC's [`warn_unused_result` attribute][GccWarnUnusedResult].
* Clang's [`warn_unused_result` attribute][ClangWarnUnusedResult].

[Cpp17Nodiscard]: https://en.cppreference.com/w/cpp/language/attributes/nodiscard
[RustMustUse]: https://doc.rust-lang.org/reference/attributes/diagnostics.html#the-must_use-attribute
[GccWarnUnusedResult]: https://gcc.gnu.org/onlinedocs/gcc/Common-Function-Attributes.html#Common-Function-Attributes
[ClangWarnUnusedResult]: https://clang.llvm.org/docs/AttributeReference.html#nodiscard-warn-unused-result

### Cross-Language Comparison Table

|Language      |Attribute           |Applies to         |Analysis                   |Diagnostic
|--------------|--------------------|-------------------|---------------------------|----------
|C++17         |`[[nodiscard]]`     |Types and functions|Syntax-level               |Warning
|Rust          |`#[must_use]`       |Types and functions|Type-level and syntax-level|Warning
|C (GCC, Clang)|`warn_unused_result`|Functions          |Syntax-level               |Warning
|D (DIP 1038)  |`@mustUse`        |Types and functions|Type-level and syntax-level|Error

The distinction between syntax-level and type-level analysis is discussed in
the [Description](#description) section below.

## Description

`@mustUse` is a compiler-recognized user-defined attribute declared in the D
runtime module `core.attribute`. It takes no arguments.

An expression is considered to be discarded if and only if either of the
following is true:

* it is the top-level *Expression* in an *ExpressionStatement*, or
* it is the *AssignExpression* on the left-hand side of the comma in a
  *CommaExpression*.

It is a compile-time error to discard an expression if either of the following
is true:

* it is a call to a function whose declaration is annotated with
  `@mustUse`, or
* it is not an assignment expression, and its type is an aggregate (a `struct`,
  `union`, `class`, or `interface`) whose declaration is annotated with
  `@mustUse`.

Note that the former is a syntax-level check, while the latter is a type-level
check. This means that the *value* returned from a `@mustUse`-annotated
function may be discarded as long as the function call itself is
enclosed in some other expression. For example:

```d
// un-annotated type
struct Result { int n; }

// @mustUse-annotated function
@mustUse Result func() { return Result(0); }

void main()
{
    import std.stdio: writeln;

    // no error: the return value of func is "used" by the comma expression
    (writeln("side effect"), func());
}
```

By contrast, a value of a `@mustUse`-annotated type will always cause an
error if it is discarded, regardless of the expression that discards it:

```d
// @mustUse-annotated type
@mustUse struct Result { int n; }

// un-annotated function
Result func() { return Result(0); }

void main()
{
    import std.stdio: writeln;

    // error: the return type of func is also the type of the comma expression
    (writeln("side effect"), func());
}
```

In all cases, an error resulting from `@mustUse` can be suppressed by
prepending `cast(void)` to the offending expression, since a *CastExpression*
is not a function call and `void` is not an aggregate type annotated with
`@mustUse`.

Using `@mustUse` has no effects on the semantics of a program other than
those described above. In particular:

* `@mustUse` is not part of the type of any symbol to which it is applied to and does
  not participate in name mangling.
* A `@mustUse` annotation on a function or aggregate declaration does not
  apply to declarations inside that function or aggregate's body (that is,
  `@mustUse` does not implicitly propagate from outer scopes to inner ones).
* `@mustUse` has no semantic effect on declarations other than aggregate and
  function declarations.
* `@mustUse` has no effect on declarations other than the ones to which it is
  explicitly applied. In particular, a `@mustUse` annotation on a class is
  not inherited by classes that inherit from it, and a `@mustUse` annotation
  on a virtual function is not inherited by functions that override it.

### Design Goals and Possible Alternatives

The design for `@mustUse` described above was chosen to achieve the best
possible balance among the following goals:

1. **Simplicity of specification.** A language feature that is difficult to
   specify precisely is likely to also be difficult to learn and difficult to
   use correctly.

2. **Simplicity of implementation.** A language feature that has a complex
   implementation is likely to suffer from implementation bugs. A complex
   implementation also increases the burden on compiler maintainers, which
   makes future improvements to both the language and its compilers more
   difficult to achieve.

3. **Lack of friction.** A language feature is only useful in practice to the
   extent that programmers choose to use it. If a feature places any
   obstacles in the programmer's way (such as additional compile-time errors),
   it must offer benefits commensurate with that cost.

A few possible alternative designs, along with the reasons for their rejection,
are discussed below.

#### Dataflow analysis

As noted above, the design presented in this DIP allows "false negatives":
expressions in which the return value of a `@mustUse`-annotated function is
discarded without triggering a compile-time error.

One way to avoid false negatives would be to have the compiler perform dataflow
analysis to determine which expressions could potentially evaluate to the
return value of a `@mustUse`-annotated function, and issue an error if any of
those expressions were discarded.

Whole-program dataflow analysis would completely eliminate false negatives, but
its costs in terms of both implementation complexity and compile-time
performance would likely be prohibitive. Intra-procedural dataflow analysis, on
the other hand, would be more feasible to implement, but would still allow
false negatives in some cases. For example:

```d
@mustUse int dontIgnoreMe() { return 42; }
int identity(int n) { return n; }

void main()
{
    // no error: identity is not annotated with @mustUse
    identity(dontIgnoreMe());
}
```

In either case, the implementation complexity of a dataflow-analysis-based
design would be far greater than that of the design proposed by this DIP. If
written with sufficient detail to fully describe the implementation's behavior,
the specification's complexity would be significantly greater as well.

#### `mustUse` type qualifier

Another way to avoid false negatives would be to make `mustUse` a type
qualifier rather than an attribute. This would allow the type-level check,
which does not suffer from false negatives, to be applied to return values of
*any* type, not just user-defined aggregate types, and eliminate the need for
the syntax-level check.

Because type qualifiers are an established concept in the language
specification with an existing implementation, the specification and
implementation complexity of a `mustUse` type qualifier would likely be
relatively low.

Unfortunately, making `mustUse` a type qualifier would introduce significant
friction in the form of "false positives": expressions that do not discard a
`mustUse`-qualified value, but are diagnosed by the compiler as though they
do. For example:

```d
mustUse(int) dontIgnoreMe() { return 42; }
int identity(int n) { return n; }

void main()
{
    // error: cannot pass mustUse(int) argument to int parameter
    auto result = identity(dontIgnoreMe());
}
```

This friction would be particularly severe for programmers attempting to make
newly-written D code that uses `mustUse` work together with existing D code
that does not—in other words, for `mustUse`'s earliest adopters. The most
likely result would be a language feature that, while theoretically more sound,
would provide less utility to D programmers in practice than the design
proposed by this DIP.

#### Type-only `@mustUse` attribute

Perhaps the easiest way to avoid false negatives would be to simply drop the
syntax-level check and recognize the `@mustUse` attribute only when applied
to an aggregate type. The resulting design would be strictly less complex in
terms of both specification and implementation than the one this DIP proposes,
and would not introduce any risk of false positives.

However, choosing this design would introduce friction of a different sort: a
higher barrier to adoption. Consider the following example:

```d
int dontIgnoreMe() { return 42; }
void useResult(int n) {}

void ok()
{
    useResult(dontIgnoreMe());
}

void noGood()
{
    dontIgnoreMe();
}
```

Suppose that we would like to use `@mustUse` to ensure that the return value
of `dontIgnoreMe` is not discarded. With the design for `@mustUse` proposed
by this DIP, there are two ways we can go about it:

1. We can annotate `dontIgnoreMe` with `@mustUse`.
2. We can change the return type of `dontIgnoreMe` to a `@mustUse`-annotated
   type.

If we choose (1), the only other change we need to make is to update the call
site in `noGood`, which discards the return value. The call site in `ok` is
already correct and can be left alone.

If we choose (2), however, we must update *every* call site of `dontIgnoreMe`,
whether it discards the return value or not. If our program were larger, the
prospect of doing so might discourage us from using `@mustUse` in the first
place.

Making `@mustUse` meaningful only as a type attribute, without the
syntax-level check, would leave D programmers with only choice (2). As a
result, while programmers who did choose to use `@mustUse` might be better
off, there would be fewer of them than there would be if choice (1) were also
available. On balance, the author believes that the design proposed by this DIP
is likely to be more useful to more D programmers in practice than one which
excludes the syntax-level check.

The experience of other languages appears to support this hypothesis. In C and
C++, the syntax-level check was considered useful enough to be worth including
on its own. In Rust, the syntax-level check was considered useful enough to be
worth including (via [RFC 1940][Rfc1940]) even with the more rigorous
type-level check already in the language.

Nevertheless, if the language maintainers are not prepared to accept the
syntax-level check at this time, accepting `@mustUse` as an attribute only
for types, with a type-level check, is a compromise that would preserve many of
the benefits of this DIP's proposal. In that case, to allow for the possibility
of future expansion, annotating a function with `@mustUse` should result in a
compile-time error.

[Rfc1940]: https://rust-lang.github.io/rfcs/1940-must-use-functions.html

## Breaking Changes and Deprecations

No breaking changes or deprecations are anticipated.

## Reference

* [Issue 3882 - Unused result of pure functions][Issue3882]
* [Issue 5464 - Attribute to not ignore function result][Issue5464]
* [Issue 20165 - Add standard @nodiscard attribute for functions][Issue20165]
* [xxxInPlace or xxxCopy?][Thread1] (D.General)
* [There is anything like nodiscard attribute in D?][Thread2] (D.Learn)
* [Idiomatic way to express errors without resorting to exceptions][Thread3]
  (D.Learn)
* [Vladimir Panteleev's `Success` type.][SuccessType]

[Issue3882]: https://issues.dlang.org/show_bug.cgi?id=3882
[Issue5464]: https://issues.dlang.org/show_bug.cgi?id=5464
[Issue20165]: https://issues.dlang.org/show_bug.cgi?id=20165
[Thread1]: https://forum.dlang.org/thread/ih7sfi$1q6f$1@digitalmars.com
[Thread2]: https://forum.dlang.org/thread/rzfshzfrxrlbxyvcngke@forum.dlang.org
[Thread3]: https://forum.dlang.org/thread/hhpqmifgjslpzbzfauab@forum.dlang.org
[SuccessType]: https://forum.dlang.org/thread/apphidjekselhhctclgr@forum.dlang.org#post-fttcdxppydmkvusmrdgh:40forum.dlang.org

## Copyright & License
Copyright (c) 2020 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews
### Community Review Round 1

[Reviewed Version](https://github.com/dlang/DIPs/blob/58b29a85fdf3cbf3521235e40f2a66e141e856c2/DIPs/DIP1038.md)

[Discussion](https://forum.dlang.org/post/zlanrlqqjjtvfwbymppn@forum.dlang.org)

[Feedback](https://forum.dlang.org/post/qptthjobogooocleizvo@forum.dlang.org)

The following points were raised in the feedback thread:

* How will the cast semantics interact with `@safe`? The DIP author responded that `cast(void)` is always `@safe`.
* `@nodiscard` should not apply to `void` functions. The DIP author agreed but noted that it's not transitive and can never be inferred, so the solution is simply for programmers not to apply it to `void` functions. The equivalent features in C++ and Rust do not make this exception.
* Regarding the "Error handling without exceptions" subsection of the Rationale:
  - It's possible to build a D interface that throws exceptions when binding to other languages
  - The commenter is aware of at least one package marked "optimized for fast execution" that throws
  - The numbers are inaccurate. The DIP author is aware of this, but the effort required for complete accuracy is prohibitive, and he believes the proposal is stronger even with inaccurate numbers than without.
* The DIP doesn't mention constructors. The DIP author replied that the rules cover this implicitly since constructors are functions.
* The DIP should mention if `@nodiscard` applies when the attribute is attached to a type and a constructor is called for that type. The DIP author replied that a call to a type's constructor is an expression of that type and, therefore, when `@nodiscard` is applied to the type, the expression is non-discardable.
* The DIP does not cover template functions with auto-inferred types, the return type of which can be `void` or not depending upon the template arguments. The DIP author replied he could not imagine a need to mark such a function as `@nodiscard`.

### Final Review

[Reviewed Version](https://github.com/dlang/DIPs/blob/ab056150975a9a8db5b5da3dbffdd81529802a49/DIPs/DIP1038.md)

[Discussion](https://forum.dlang.org/post/nfyfsmenjwzhbezffori@forum.dlang.org)

[Feedback](https://forum.dlang.org/post/ipfmxoimroobpzymruzy@forum.dlang.org)

There were only two actionable items of feedback in this round. One was about an ambiguity in the text. The other:

Given a function `@nodiscard int foo` called in, e.g., a ternary operation `a ? b : foo()`, there is no error raised about the return value of `foo` being ignored. The reviewer sees this as a critical shortcoming that should be addressed. The DIP author responded that this behavior is identical to that of GCC, C++17, and Rust. He explained that such error cases could be detected if the annotation were a type qualifier, e.g., `@nodiscard(int) foo`, but that can lead to undesirable errors. He suggested that syntax-level checks could be removed from the proposal if they are deemed inadequate.

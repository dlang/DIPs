# foreach auto ref

| Field           | Value                                                           |
|-----------------|-----------------------------------------------------------------|
| DIP:            | 1022                                                            |
| Review Count:   | 1                                                               |
| Author:         | Ate Eskola (ajieskola@gmail.com)                                |
| Implementation: |                                                                 |
| Status:         | Post-Community Round 1                                          |

## Abstract

This DIP proposes that `foreach` loops with the `ref` keyword applied to the element variable should only be legal when
the elements of the range have memory addresses. It also proposes that current usages `ref` may be replaced with `auto ref`
to retain the current behavior. This is to ensure that `foreach` will iterate by reference,
while still allowing iteration over a range of non-copyable elements without explicit need to adapt the code[[1](#issue4707)].

## Contents
* [Rationale](#rationale)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Alternatives](#alternatives)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale

There are currently two ways to iterate over a range with `foreach`: by value and by reference.

One is forced to iterate the range by reference if the element type is a `struct` with
a disabled postblit since iteration by value attempts to copy the element variable.
As of DMD 2.087.0, any range can be iterated by reference. If the elements of the
iterated range are rvalues[[2](#rvalues)], `ref` will simply be ignored. A pull request[[3](#pr)]
was submitted to disallow this behavior.

In every place where `foreach (ref <...>)` presently iterates by value, a compiler
error would have resulted after merging that pull request. However, it did not
produce the desired effect due to code breakage without a deprecation
period and the lack of way to write the `foreach` loop when the current behavior
of `ref` is desired. Even so, the pull request was approved by Andrei
Alexandrescu, who clearly expressed a dislike of `ref` meaning iteration sometimes
by reference and sometimes by value[[4](#andreionref)]:

    I think it's important for many parts of the language to not naively bind references to rvalues.
    In foreach the most obvious ill effect is that people believe they change elements of a range but
    they don't. cc @WalterBright and @RazvanN7 for reviewing implementation.

This behavior is a weakness because the user cannot be sure that `foreach(ref ...)`
will iterate by reference. If a range with non-reference semantics is accidently
passed to such a loop, it will be iterated by value, which is likely to be unexpected.

However, iteration by reference may be needed even when there is no intention to change the loop variable because ranges
of non-copyable `struct`s---those with a disabled postblit or copy constructor---cannot be iterated by value.
In such cases, the programmer will probably not want a compiler error when elements are changed to
copyable rvalues. Thus, when iterating by reference, the programmer should still be able to explicitly let the program fall back
to iteration by value when iteration by reference cannot be implemented.

Non-copyable `struct`s are an excellent tool when designing containers adhering to the RAII (Resource-
Acquisition-Is-Initialization) principle. The EMSI-Containers package[[5](#emsi)] provides good examples of such non-copyable RAII containers.
When one has many such containers, it is natural to use ranges to iterate over them.
See [EMSI-Containers example below](example-using-emsi-containers).

For function return values with a similar problem, the distinction between `ref` and `auto ref`[[6](#autoref)]
already exists. For consistency, `auto ref` applied to the element variable of a `foreach` loop
should be considered the best candidate as a means to ensure correct behavior when falling back
to iteration by value is desired, and `ref` should be used when falling back is not desired.

## Description

This DIP proposes that, when encountering a `foreach` loop with the `ref` keyword applied to the element variable, as in:
```D
foreach (ref loopVariable1; aggregate)
{
    loopVariable1.doSomething();
}
```

...then if one or more elements of `aggregate` are rvalues[[2](#rvalues)], a deprecation
message must be emitted including the suggestion to annotate `loopVariable1` with the
`auto ref` keyword instead.

This DIP also proposes that when the compiler encounters a `foreach` statement
such as this:

```D
foreach (auto ref loopVariable2; nonAliasSeqAggregate)
{
    loopVariable2.doSomething();
}
```

...then if elements of `nonAliasSeqAggregate` are lvalues [[2](#rvalues)], the
above loop has exactly the same semantics as if it were written like this:

```D
foreach (ref loopVariable2; nonAliasSeqAggregate)
{
    loopVariable2.doSomething();
}
```

Otherwise, the statement is interpreted as:

```D
foreach (loopVariable2; nonAliasSeqAggregate)
{
    loopVariable2.doSomething();
}
```

If the compiler encounters a `foreach` statement such as this:

```D
foreach (auto ref loopVariable3; anAliasSequence)
{
    loopVariable3.doSomething();
}
```

...then it must check that all members of `anAliasSequence` are values
(as opposed to, e.g., type names or module names). If that check fails,
an error message must result. Otherwise, each iteration in which `loopVariable3`
aliases to an lvalue must be compiled with reference semantics, and each iteration
where `loopVariable3` aliases to an rvalue must be compiled as if written like this:

```D
foreach (__HIDDEN_ALIAS; anAliasSequence)
{
    auto loopVariable3 = __HIDDEN_ALIAS;
    loopVariable3.doSomething();
}
```

Note that the above behavior for rvalues is intentionally different from the
behavior of `foreach` without `ref` or `auto ref`. These semantics are
proposed by this DIP because they allow `loopVariable3` to always be an
lvalue from the user's perspective.

`auto ref` should work in both templated and non-templated functions. It should
be allowed in `static foreach`, but with no effect, as elements
of compile-time aggregates can never be lvalues.

### Example using EMSI-Containers

Briefly, `containers.DynamicArray` is an array that automatically allocates
and deallocates the memory it requires without relying on garbage collection. It
does not allow itself to be copied in order to protect the memory from being accidently
aliased to a dangling reference.

```D
import std.algorithm;
import std.range;
import std.stdio;
import containers;

// A helper function to construct an EMSI containers dynamic array
// within a single statement
auto dynamicArray(R)(R range)
{
    auto result = DynamicArray!(ElementType!R).init;
    foreach (el; range) result.put(el);
    return result;
}

void writeDeepLengthA(Roi)(Roi rangeOfIterables)
{
    typeof(rangeOfIterables.front.front) sum;
    foreach (iterable; rangeOfIterables) sum += iterable.length;
    sum.writeln;
}

void writeDeepLengthB(Roi)(Roi rangeOfIterables)
{
    typeof(rangeOfIterables.front.front) sum;
    foreach (ref iterable; rangeOfIterables) sum += iterable.length;
    sum.writeln;
}

// Enabled by this DIP
void writeDeepLengthC(Roi)(Roi rangeOfIterables)
{
    typeof(rangeOfIterables.front.front) sum;
    foreach(auto ref iterable; rangeOfIterables) sum += iterable.length;
    sum.writeln;
}

void main()
{
    // Elements of this range can be copied but have no address
    auto unaddressedElements = iota(0, 10).map!(i => iota(0, i));

    // Vice-versa
    auto uniqueElements =
    [
        only(7, 2, 29, 30).dynamicArray,
        only(11, 9, 0).dynamicArray,
        takeNone!(int[]).dynamicArray,
        only(3, 30, 14, 19, 4, 0, 9).dynamicArray
    ];

    // Ok, prints 45
    unaddressedElements.writeDeepLengthA;

    // Error: struct `containers.dynamicarray.DynamicArray!(int, Mallocator, false).DynamicArray`
    // is not copyable because it is annotated with @disable
    uniqueElements.writeDeepLengthA;

    // prints 45, deprecated by this DIP
    unaddressedElements.writeDeepLengthB;

    // Ok, prints 14
    uniqueElements.writeDeepLengthB;

    // Ok, prints 45
    unaddressedElements.writeDeepLengthC;

    // Ok, prints 14
    uniqueElements.writeDeepLengthC;
}
```
## Breaking Changes And Deprecations

This DIP will break an unknown amount of code. The use of `foreach(ref <...>)`
where elements of the aggregate are rvalues[2] will be deprecated for the standard
deprecation period. The migration path will be to replace such loops with either
`foreach(<...>)` or `foreach(auto ref <...>)`.

## Alternatives

- `ref` could retain the current behavior and a different syntax could be used
    to do what `ref` should do according to this proposal. The advantage is that no
    deprecation period is required. The disadvantage is that the semantics of `ref`
    will remain inconsistent between function signatures and `foreach` loops.

- While deprecating `ref` as described by this paper, instead of implementing
    `foreach(auto ref ...)` a library solution could be implemented that takes an
    `alias` compile-time parameter and chooses the iteration method on behalf of the programmer.
    `std.algorithm.iteration.each` [[7](#algo)] would be a good candidate. This
    concept has the following disadvantages:
    - Error messages become harder to read than with normal loops,
    - Using `goto`, labeled `break` and `continue`, and `return` inside the loop
        body to jump to elsewhere in the calling function becomes impossible.
    - Needless heap allocations are caused if local variables outside the loop body
        are accessed.

    Note that such a library function is not mutually exclusive with implementing `foreach`
    with `auto ref`.

- The compiler could try to detect if a `foreach` loop by value can be silently rewritten
    with reference semantics without effect to program output and allow non-copyable
    range elements if this is the case. This was originally suggested by this DIP, but it
    was determined that this approach cannot be practically implemented without restricting
    otherwise valid code in the `foreach` body.

## Reference

- <a name="issue4707"></a>A request for this feature in bugzilla:
    * https://issues.dlang.org/show_bug.cgi?id=4707

- <a name="rvalues"></a>Meaning of rvalues and lvalues explained
    * http://ddili.org/ders/d.en/lvalue_rvalue.html

- <a name="pr"></a>A pull request for DMD to disallow iteration by reference when the aggregate
 does not support it:
    * https://github.com/dlang/dmd/pull/8437

- <a name="andreionref"></a>Approval of the pull request to disallow fake iteration by reference by Andrei Alexandrescu:
    * https://github.com/dlang/dmd/pull/8437#pullrequestreview-146141924

- <a name="emsi"></a>Emsi-containers GitHub repository
    * https://github.com/dlang-community/containers

- <a name="autoref"></a>`auto ref` language specification for function return values:
    * https://dlang.org/spec/function.html#auto-ref-functions

- <a name="algo"></a>std.algorithm.iteration.each documentation
    * https://dlang.org/phobos/std_algorithm_iteration.html#.each

- <a name="aliasseq"></a>specification of iteration over alias sequences:
    * https://dlang.org/spec/statement.html#foreach_over_tuples


## Copyright & License

Copyright (c) 2019 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

The DIP Manager will supplement this section with a summary of each review stage
of the DIP process beyond the Draft Review.

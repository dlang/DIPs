# foreach auto ref

| Field           | Value                                                           |
|-----------------|-----------------------------------------------------------------|
| DIP:            | 1022                                                            |
| Review Count:   | 2                                                               |
| Author:         | Ate Eskola (ajieskola@gmail.com)                                |
| Implementation: |                                                                 |
| Status:         | Final Review                                                    |

## Abstract

This DIP proposes that `foreach` loops with the `ref` keyword applied to the element variable should only be legal when
the elements of the range have memory addresses. It also proposes that current usages of `ref` may be replaced with `auto ref`
to retain the current behavior. This is to ensure that `foreach` will iterate by reference,
while still allowing iteration over a range of non-copyable elements without explicit need to adapt the code[[1](#issue4707)].

## Contents
* [Rationale](#rationale)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Alternatives](#alternatives)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale

There are currently two ways to iterate over a range with `foreach`: by value and by reference.
As of DMD 2.088.0, any range can be iterated by reference. If the elements of the
iterated range are rvalues[[2](#rvalues)], `ref` will simply be ignored.

This behavior is problematic because the user cannot be sure that `foreach(ref ...)`
will always iterate by reference. If a range with non-reference semantics is accidently
passed to such a loop, it will be iterated by value, likely an unexpected outcome.
Preventing compilation in these cases will avoid the issue.

Such a change to the semantics of `foreach(ref ...)` introduces another problem.
Iteration by reference may be needed even when there is no intention to change the element variable, because ranges
of a non-copyable `struct`---those with a disabled postblit or copy constructor---cannot be iterated by value.
In such cases, the programmer will not want a compiler error when elements are changed to
copyable rvalues. Thus, when iterating by reference, the programmer should still be able to explicitly let the program fall back
to iteration by value when iteration by reference cannot be implemented.

For function return values with a similar problem, the distinction between `ref` and `auto ref`[[6](#autoref)]
already exists. For consistency, `auto ref` applied to the element variable of a `foreach` loop
should be considered the best candidate as a means to ensure correct behavior when falling back
to iteration by value is desired, and `ref` should be used when falling back is not desired.

A simple example:

```D
struct UniqueInt
{
    int theInt;
    @disable this(this);
    alias theInt this;
}

void main()
{
    auto rangeOfUniques = [UniqueInt(0), UniqueInt(1), UniqueInt(2), UniqueInt(3), UniqueInt(4)];

    // Compiles, does not change the elements
    foreach(el; 0 .. 5) el = UniqueInt(15);
    // Will not compile due to disabled postblit constructor
    foreach(el; rangeOfUniques) el = UniqueInt(15);

    // Compiles, does not change the elements. Proposed to be deprecated by this DIP.
    foreach(ref el; 0 .. 5) el = UniqueInt(15);
    // Compiles and changes the elements
    foreach(ref el; rangeOfUniques) el = UniqueInt(15);

    // Proposed by the DIP to compile, and not to change the elements.
    foreach(auto ref el; 0 .. 5) el = UniqueInt(15);
    // Proposed by the DIP to compile, and to change the elements.
    foreach(auto ref el; rangeOfUniques) el = UniqueInt(15);
}
```

A non-copyable `struct` is an excellent tool when designing containers adhering to the RAII (Resource-
Acquisition-Is-Initialization) principle. The EMSI-Containers package[[5](#emsi)] provides good examples of such non-copyable RAII containers.
When one has many such containers, it is natural to use ranges to iterate over them.
See [EMSI-Containers example below](example-using-emsi-containers).

## Description

This DIP proposes changes to the behavior of `foreach` loops that have the `ref` keyword applied
to their element variable and allowing `auto ref` to be applied in its place. The nature of the
behavioral change depends on whether the loop aggregate is an alias sequence.

### Non-static foreach over range or using opApply

When the compiler encounters a statement such as this example:

```D
foreach (ref loopVariable; aggregate)
{
    loopVariable.doSomething();
}
```

...then if one or more elements of `aggregate` are rvalues[[2](#rvalues)], a deprecation
message must be emitted including the suggestion to annotate `loopVariable` with the
`auto ref` keyword instead.

When the compiler encounters a `foreach` statement such as this:

```D
foreach (auto ref loopVariable; aggregate)
{
    loopVariable.doSomething();
}
```

...then if elements of `aggregate` are lvalues [[2](#rvalues)], the
above loop has exactly the same semantics as if it were written like this:

```D
foreach (ref loopVariable; aggregate)
{
    loopVariable.doSomething();
}
```

Otherwise, the statement is interpreted as:

```D
foreach (loopVariable; aggregate)
{
    loopVariable.doSomething();
}
```

If `loopVariable` is an array literal [[3](#arrayliterals)] or a number range
[[4](#numberranges)], it must be considered a range of rvalues.

### Non-static foreach over an alias sequence

If the compiler encounters a `foreach` statement such as this:

```D
foreach (ref loopVariable; anAliasSequence)
{
    loopVariable.doSomething();
}
```

...then the current behavior should be retained.

If the compiler encounters a `foreach` statement such as this:

```D
foreach (auto ref loopVariable; anAliasSequence)
{
    loopVariable.doSomething();
}
```

...then it must check that all members of `anAliasSequence` are values
(not aliases to, e.g., type names or module names). If that check fails,
an error message must result. Otherwise, each iteration in which `loopVariable`
aliases to an lvalue must be compiled with reference semantics, and each iteration
where `loopVariable` aliases to an rvalue must be compiled as if written like this:

```D
foreach (__HIDDEN_ALIAS; anAliasSequence)
{
    auto loopVariable = __HIDDEN_ALIAS;
    loopVariable.doSomething();
}
```

Note that the above behavior for rvalues is intentionally different from the
behavior of `foreach` without `ref` or `auto ref`. These semantics are
proposed by this DIP because they allow `loopVariable` to always be an
lvalue from the user's perspective.

### static foreach

No changes are suggested for `static foreach` loops. `auto ref` will not work
with them.

### Example using EMSI-Containers

Briefly, `containers.DynamicArray` is an array that automatically allocates
and deallocates the memory it requires without relying on garbage collection. It
does not allow itself to be copied in order to protect the memory from being accidently
aliased to a dangling reference.

```D
import std.algorithm;
import std.range;
import std.stdio;
import containers;

// A helper function to construct an EMSI containers dynamic array
// within a single statement
auto dynamicArray(R)(R range)
{
    auto result = DynamicArray!(ElementType!R).init;
    foreach (el; range) result.put(el);
    return result;
}

auto getDeepLength(Roi)(Roi rangeOfIterables)
{
    typeof(rangeOfIterables.front.front) sum;
    foreach (iterable; rangeOfIterables) sum += iterable.length;
    return sum;
}

auto getDeepLengthByRef(Roi)(Roi rangeOfIterables)
{
    typeof(rangeOfIterables.front.front) sum;
    foreach (ref iterable; rangeOfIterables) sum += iterable.length;
    return sum;
}

// Enabled by this DIP
auto getDeepLengthByAny(Roi)(Roi rangeOfIterables)
{
    typeof(rangeOfIterables.front.front) sum;
    // foreach(auto ref iterable; rangeOfIterables) sum += iterable.length;
    return sum;
}

void main()
{
    // Elements of this range can be copied but have no address
    auto unaddressedElements = iota(0, 10).map!(i => iota(0, i));

    // Vice-versa
    auto uniqueElements =
    [
        only(7, 2, 29, 30).dynamicArray,
        only(11, 9, 0).dynamicArray,
        takeNone!(int[]).dynamicArray,
        only(3, 30, 14, 19, 4, 0, 9).dynamicArray
    ];

    // Ok, prints 45
    unaddressedElements.getDeepLength.writeln;

    // Error: struct `containers.dynamicarray.DynamicArray!(int, Mallocator, false).DynamicArray`
    // is not copyable because it is annotated with @disable
    uniqueElements.getDeepLength.writeln;

    // prints 45, deprecated by this DIP
    unaddressedElements.getDeepLengthByRef.writeln;

    // Ok, prints 14
    uniqueElements.getDeepLengthByRef.writeln;

    // Ok, prints 45
    unaddressedElements.getDeepLengthByAny.writeln;

    // Ok, prints 14
    uniqueElements.getDeepLengthByAny.writeln;
}
```
## Breaking Changes And Deprecations

This DIP will break an unknown amount of code. The use of `foreach(ref <...>)`
where elements of the aggregate are rvalues[2] will be deprecated for the standard
deprecation period. The migration path will be to replace such loops with either
`foreach(<...>)` or `foreach(auto ref <...>)`.

However, in cases when the loop body of a deprecated `foreach(ref <...>)`
contains `__traits(isRef, <element name>)`, the loop must be redesigned, as the
return value of the expression will change from `true` to `false`. Example:

```
struct Iota
{
    int front;
    int cap;

    this(int a){cap = a;}
    void popFront(){front++;}
    bool empty(){return front >= cap;}
}

foreach(ref i;Iota(5)){ // current behavior:
    static assert(__traits(isRef,i));
}
foreach(auto ref i;Iota(5)){ // new behavior:
    static assert(!__traits(isRef,i));
}
```

The number of deprecation messages when using libraries can be cut down by adding
a compiler preview switch a few releases before the actual deprecation, but this
is not required by the DIP.

## Alternatives

- `ref` could retain the current behavior and a different syntax could be used
    to do what `ref` should do according to this proposal. The advantage is that no
    deprecation period is required. The disadvantage is that the semantics of `ref`
    will remain inconsistent between function signatures and `foreach` loops. The
    DIP author recommends considering this alternative behaviour of the
    `-preview=rvaluerefparam` compiler switch is planned to become the default.

- While deprecating `ref` as described by this proposal, instead of implementing
    `foreach(auto ref ...)` a library solution could be implemented that takes an
    `alias` compile-time parameter and chooses the iteration method on behalf of the programmer.
    `std.algorithm.iteration.each` [[7](#algo)] would be a good candidate. This
    concept has the following disadvantages:
    - Error messages become harder to read.
    - Using `goto`, labeled `break` and `continue`, and `return` inside the loop
        body to jump to elsewhere in the calling function becomes impossible.
    - Needless heap allocations are caused if local variables outside the loop body
        are accessed.

    Note that such a library function is not precluded by implementing `foreach`
    with `auto ref`.

- The compiler could try to detect if a `foreach` loop by value can be silently rewritten
    with reference semantics without effect to program output and allow non-copyable
    range elements if this is the case. This was originally suggested by this DIP, but it
    was determined that this approach cannot be practically implemented without restricting
    otherwise valid code in the `foreach` body.

## Reference

- <a name="issue4707"></a>A request for this feature in bugzilla:
    * https://issues.dlang.org/show_bug.cgi?id=4707

- <a name="rvalues"></a>Meaning of rvalues and lvalues explained
    * http://ddili.org/ders/d.en/lvalue_rvalue.html

- <a name="arrayliterals"></a>Array literals explained
    * https://dlang.org/spec/expression.html#array_literals

- <a name="numberranges"></a>Meaning of number ranges
    * http://ddili.org/ders/d.en/ranges.html, at section "Phobos ranges"

- <a name="emsi"></a>Emsi-containers GitHub repository
    * https://github.com/dlang-community/containers

- <a name="autoref"></a>`auto ref` language specification for function return values:
    * https://dlang.org/spec/function.html#auto-ref-functions

- <a name="algo"></a>std.algorithm.iteration.each documentation
    * https://dlang.org/phobos/std_algorithm_iteration.html#.each

- <a name="aliasseq"></a>specification of iteration over alias sequences:
    * https://dlang.org/spec/statement.html#foreach_over_tuples


## Copyright & License

Copyright (c) 2019 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

### Community Review Round 1

[Reviewed Version](https://github.com/dlang/DIPs/blob/8f3c24df2acc0f3554875a9decf6314508b72d8a/DIPs/DIP1022.md)

[Discussion](https://forum.dlang.org/post/nphczadgbedjzowuhwnz@forum.dlang.org)

Points of criticism for this revision:
    - the EMSI-containers example has too much irrelevant code and should be replaced by something more minimal and focused
    - an example should be added at the top demonstrating the current problem
    - the necessity of deprecating `ref` with rvalues was questioned, with some debate
    - the line "It should be allowed in static foreach, but with no effect" should be removed or changed
    - the DIP does not take into account `-preview=rvalueRefParam`
The DIP author agreed to consider modifying the examples. After much debate over the details related to the remaining points of criticism, the DIP author concluded that significant revisions are required.

### Community Review Round 2

[Reviewed Version](https://github.com/dlang/DIPs/blob/089816bc47ee3d1df06d10aa2af943d3e70e6161/DIPs/DIP1022.md)

[Discussion](https://forum.dlang.org/post/kgfzzttzjwnmhmqcarvu@forum.dlang.org)

Feedback was focused on the need for more clarity. The Rationale should better clarify the current state of `ref` in `foreach`and how it's broken, and summarize the proposed change. The suggestion was also made that description in the Rationale of the behavior of the prior pull request is incorrect and should be corrected or removed. Two specific use cases the DIP does not cover were brought to the author's attention. It was also suggested the that DIP's claim that "current usages of `ref` may be replaced with `auto ref` to retain the current behavior" is incorrect.

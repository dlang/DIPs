# Named parameters

| Field           | Value                                                           |
|-----------------|-----------------------------------------------------------------|
| DIP:            | 1020                                                            |
| Review Count:   | 2                                                               |
| Author:         | Richard Andrew Cattermole <firstname@lastname.co.nz>            |
| Implementation: |                                                                 |
| Status:         | Superseded                                                      |

## Abstract
This DIP proposes the addition of named parameters to the language to supplement function and template parameters. This feature encourages arguments being passed to functions using names that represent publicly accessible information while discouraging the passing of internal information. Named parameters introduce an additional abstraction level beyond unnamed parameters as a tool to enhance the modeling power of the D programming language.

## Contents
* [Rationale](#rationale)
* [Prior Work](#prior-work)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Reference](#reference)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale
Named arguments have seen adoption as a feature in many popular languages as of 2019 according to the [TIOBE index](https://www.tiobe.com/tiobe-index/). These languages include Ada, Kotlin, C#, Python, R, Ruby, and Scala, [as seen in this Wikipedia article](https://en.wikipedia.org/wiki/Named_parameter#Use_in_programming_languages). They have been used in system APIs on OS X in a form that does not support argument reordering, [via Objective-C](https://developer.apple.com/library/archive/documentation/Cocoa/Conceptual/ProgrammingWithObjectiveC/DefiningClasses/DefiningClasses.html#//apple_ref/doc/uid/TP40011210-CH3-SW5%7CiOS).

Named arguments have been requested for D, e.g. [in this forum thread](https://forum.dlang.org/post/eefptgwdqhsbxjqjjmpy@forum.dlang.org) and [this one](https://forum.dlang.org/post/mp9l9m$1cjt$2@digitalmars.com), but without agreement on implementation details. For example, some users have disliked the idea that arguments may be passed to a function in any order, as can be seen [in this discussion](https://forum.dlang.org/post/bqyzgobnvrtrapcawguw@forum.dlang.org) and also [in this one](https://forum.dlang.org/post/m1h8if$223r$1@digitalmars.com). The primary aim of this proposal is to aid in readability and the implementation of tooling.

## Prior Work

### [Dyalect](https://github.com/vorov2/dyalect/wiki/Functions#named_arguments)
**Name:** Named arguments\
**Argument syntax:** ``Identifier : Argument``\
**Parameter syntax:** no parameter syntax\
**Semantic behavior:** Named and unnamed arguments may be used together and in called in any order.

### [Common-Lisp](http://www.gigamonkeys.com/book/functions.html)
**Name:** Keyword parameters; a parameter is an identifier preceded by a colon.\
**Argument syntax:** ``:Identifier Argument``\
**Parameter syntax:** ``&key Identifier``\
**Semantic behavior:** Any order but behave weirdly when combined with optional parameters.

### [Swift](https://docs.swift.org/swift-book/LanguageGuide/Functions.html)
**Name:** Interleaved arguments; not considered "named arguments" because reordering is prohibited.\
**Argument syntax:** ``Identifier : Argument``\
**Parameter syntax:** ``Identifier : Parameter`` or ``Identifier Identifier: Parameter`` where first ``Identifier`` is argument name and second is parameter name. Can specify ``_`` to disable argument name.\
**Semantic behavior:** Optional argument name. No reordering.

### [C#](https://docs.microsoft.com/en-us/dotnet/csharp/programming-guide/classes-and-structs/named-and-optional-arguments)
**Name:** Named arguments\
**Argument syntax:** ``Identifier : Argument``\
**Parameter syntax:** no parameter syntax\
**Semantic behavior:** Can be reordered if no positional arguments follow named arguments; the name may be ommited if the position in the argument and parameter lists is the same.

### [Python](https://docs.python.org/3.7/tutorial/controlflow.html?highlight=keyword%20argument#keyword-arguments)
**Name:** Keyword arguments\
**Argument syntax:** ``Identifier = Argument``\
**Parameter syntax:** ``** Identifier``\
**Semantic behavior:** Reordering is allowed if no positional arguments follow keyword arguments.

## Description
This proposal allows for partial argument reordering and introduces a new attribute that is used to annotate function parameters that are intended to accept named arguments. To resolve named arguments to named parameters, the existing resolution algorithms are extended with new logic.

### Requirements
Named parameters touch upon function overloading and symbol resolution algorithms. Function overload resolution handles parameters via a list of levels, the changing of which "would be a major hurdle" according to Andrei Alexandrescu in his [DConf 2019 keynote](https://dconf.org/2019/talks/alexandrescu.html). As of the writing of this DIP, symbol resolution algorithms have not been described in the spec. For these reasons, this DIP will not change the existing behavior of function overloading or symbol resolution.

The side effect of this decision is that named parameters must be opt-in. Parameters must be marked as accepting named arguments, otherwise there will be no way to determine which overload to resolve.

Based on the above constraints, the general requirements for named parameters are:

1. Named parameters must not modify symbol or overload resolution. They may and should be verified when overload resolution is complete.
2. Each named parameter must be marked, e.g. with an attribute, to distinguish it from an unnamed parameter.
3. The same syntax should be use for both function and template parameters, but they may differ on semantics.

#### AssignExpression Relationship
The language allows the ``AssignExpression`` in both parameter and argument lists. Therefore, an obvious choice of syntax to mark named parameters and arguments, ``Identifier = Expression``, is not acceptable and an alternative syntax must be chosen. Further, the selected syntax should not preclude the use of default arguments or other uses of the ``AssignExpression`` in parameter and argument lists.

As such, the following requirements apply:

1. ``Identifier = Expression`` cannot be used as a syntax to mark named parameters or named arguments.
2. Any syntax selected must be compatible with ``AssignExpression``.
3. Named parameters may accept default arguments, in which case the corresponding argument may be omitted from the argument list in a function call; otherwise, the corresponding argument is required.

#### Named Template Parameters
The author is unaware of any mainstream language that supports named template parameters. Given the notion in the design of D's template syntax that a template parameter list is simply a compile-time equivalent of a function parameter list, support for named parameters in template declarations seems a natural choice.

Template parameters of instantiated templated types can be inspected outside of the template. Accessing the template arguments is done by using the ``is`` expression. It is the author's opinion that the ``is`` expression is heavily overloaded and doing too much. It should be possible to access the names of named template parameters outside of an instantiated template, but it should be done with a mechanism other than the ``is`` expression.

The requirements for named template arguments are:

1. They do not need to be accessible from the ``is`` expression.
2. They must be accessible outside of the template.

#### Compile-time Reflection
In the current language implementation, the ``is`` expression is used to reflect on the parameters of instantiated templates at compile time, but only from within the template declaration. To access function parameters, the ``is`` expression can be used outside of the function declaration in conjunction with the ``__parameters`` specialization. Traits are used to query further information, such as a parameter's storage class.

Requirements related to the compile-time reflection:

1. A new trait is necessary to filter named parameters from unnamed parameters when using the ``is`` expression with the ``__parameters`` specialization to reflect on a function's parameters.
2. A new trait is necessary to reflect on all named parameters for both functions and templates.

### Syntax
This proposal introduces new syntax to denote named parameters in function and template parameter lists, named arguments, and compile-time reflection.

#### Parameter Syntax
Different options for opt-in named parameter syntax have been proposed and discussed in the D programming language community. [DIP 88] proposed a colon to denote a named parameter, [DIP 1019] originally proposed the use of the `@named` function attribute, and this DIP originally proposed that angle brackets be used to denote named parameters. After feedback from the first round of review, this DIP has been amended to propose an new `@named` attribute with which to annotate parameters rather than functions.

The `@named` attribute prevents clashes with other existing language features like template specializations (e.g., ``T:V`` or ``T=int``). It aims to be opt-in rather than opt-out to prevent clashes with core semantics of the D programming language like function overloading. This is a per-parameter attribute which marks a parameter as requiring a named argument. It is similar to the approach used in some other languages like Python.

```d
struct Map(@named alias Function, @named InputType, @named ElementType) {
   InputType input;

   this(InputType input) {
      this.input = input;
   }

   @property {
       ElementType front() {
           return Function(input.front);
       }

       bool empty() {
           return input.empty;
       }
   }

   void popFront() {
       input.popFront;
   }
}
```

There are no new semantics associated with this syntax.

```diff
InOut:
+   @ named

TemplateParameter:
+   @ named TemplateParameter
    ...
```
#### Argument Syntax

A new syntax is added to match a named argument to a named parameter. The syntax is based upon that of [DIP 88], using the form of ``Identifier : Argument``, where ``Argument`` matches the argument type and ``Identifier`` matches the named parameter name. ``Argument`` is any expression currently accepted as a function or template argument. The syntax is the same for both functions and templates.

The order of evaluation of named function arguments follows the lexical order of the argument list (full, not subset). The order in which named function arguments are passed to the function (relative to other named and unnamed function arguments) is platform defined. This DIP proposes no modification to name mangling, but may be amended if there is an issue with platform implementation or safety.

Example of named argument usage with functions and delegates:

```d
void myFunction1(@named int x, @named int y) {
}

myFunction1(x: 10, y: 10);

void myFunction2(@named T)(@named T x, @named T y) {
}

myFunction1!(T: float)(x: 10, y: 10);
```

Example usage with a mixin template:

```d
mixin template Foo(T, @named ErrorType:Throwable) {
}

mixin Foo!(bool, ErrorType: Error);
```

The following grammar is added:

```diff
TemplateArgument:
+   NamedTemplateArgumentList

+ NamedTemplateArgumentList:
+     NamedTemplateArgument
+     NamedTemplateArgument ,
+     NamedTemplateArgument , NamedTemplateArgumentList

+ NamedTemplateArgument:
+     Identifier : TemplateArgument

ArgumentList:
+	NamedArgument
+	NamedArgument ,
+	NamedArgument , ArgumentList

+ NamedArgument:
+    Identifier : ConditionalExpression
```
#### Supporting Syntax

Two new traits are added to support type inspection. The first addition is the ``isNamedParameter`` trait, which allows a given parameter from the parameter list of a function, delegate, or a function pointer to be inspected for the presence of the `@named` attribute. This is intended to be used with the ``__parameters`` specialization of the ``is`` expression. The second addition is the ``getNamedParameters`` trait, which returns a list of all named parameters from the parameter list of a given template, function, delegate, or function pointer.

Example usage of ``isNamedParameter`` trait to inspect function parameters.

```d
void myFunction(@named int a) {
}

static if (is(typeof(func) Params == __parameters)) {
    static foreach(i, Param; Params) {
        pragma(msg, Param); // int
        pragma(msg, __traits(identifier, Params[i .. i+1])); // a
        pragma(msg, __traits(isNamedParameter, typeof(func), i)); // true
    }
}
```

``isNamedParameter`` has the same signature as ``getParameterStorageClasses``, which [is described in the specification](https://dlang.org/spec/traits.html#getParameterStorageClasses) as:

    Takes two arguments. The first must either be a function symbol, or a type that is a function, delegate or a function pointer. The second is an integer identifying which parameter, where the first parameter is 0. It returns a tuple of strings representing the storage classes of that parameter.

The difference is that ``isNamedParameter`` should return the Boolean value `true` when the parameter is a named parameter and `false` otherwise.

Example usage of ``getNamedParameters`` to inspect the parameters of a templated struct declaration and a function pointer :

```d
struct MyType(@named Info) {
}

pragma(msg, __traits(getNamedParameters, MyType)); // tuple("Info");
pragma(msg, __traits(getNamedParameters, MyType!(Info: int))); // tuple("Info");

alias MyFunction = void function(@named bool myFlag);

pragma(msg, __traits(getNamedParameters, MyFunction)); // tuple("myFlag");
```

The new trait ``getNamedParameters`` will have the following behaviors:

```d
void func1(@named X)(@named X x);
static assert([__traits(getNamedParameters, func1)] == ["X"]);
static assert([__traits(getNamedParameters, func1!int)] == ["x"]);

void func2(@named int x);
static assert([__traits(getNamedParameters, func2)] == ["x"]);

alias Func = void function(@named int x);
static assert([__traits(getNamedParameters, Func)] == ["x"]);

alias Del = void delegate(@named int x);
static assert([__traits(getNamedParameters, Func)] == ["x"]);

alias Bar = Foo!(x: 7);
static assert([__traits(getNamedParameters, Bar)] == ["x"]);
```

The additional grammar:

```diff
TraitsKeyword:
+    isNamedParameter
+    getNamedParameters
```

### Semantics

A named parameter may be assigned a default value, in which case a corresponding argument is optional. If no default value is provided, then the corresponding argument must appear in the argument list. The effect of default values for named parameters on the resolution of template instantiations and function calls is described in the [Resolution](#resolution) section.

Overload resolution for symbols (for function and template declarations) is performed without taking named parameters into account.
Ignoring named parameters in both cases means that the relevant algorithms and user code does not need to change to accommodate named parameters but they must be modified to filter them out. This is in line with the current behavior. A side effect of this is that name mangling does not need to change to accommodate these new parameters.

The following is an example of code that will fail to compile:

```d
void foo(int a) {
}

void foo(int a, @named int b) {
}

struct Bar(T) {
}

struct Bar(T, @named int Flag) {
}

foo(1); // error: matches both declarations
foo(1, b: 2); // error: matches both declarations

alias Bar1 = Bar!int; // error: matches both declarations
alias Bar2 = Bar!(int, Flag: 0); // error: matches both declarations
```
Template declarations that have named parameters expose the named parameter as a member of the template.

```d
struct MyType(@named SomeType) {
}

static assert(is(MyType!(SomeType: int) == SomeType));
```

Manifest constant templates and templated aliases are treated as if they were eponymous templates and do expose named parameters as members.

It is an error for named template parameters to share the name of any template member:

```d
template Foo(@named Bar) {
  struct Bar {} // error: named template parameter collides with declaration
}

void myFunction(@named Input)(@named Input value) {
    int value; // error: named function parameter collides with declaration

    alias Input = Something; // error: named template parameter collides with declaration
}
```

Currently, the D programming language provides limited facilities to inspect template declarations for their parameters. If a parameter's position in the parameter list is known, and if it is a value or type parameter, it can be accessed by indexing the ``AliasSequence`` returned from ``is ( Type : TypeSpecialization , TemplateParameterList )``.
This variant of ``is`` is often used as the condition expression of a `static if`, the body of which is valid only if the template is instantiated with arguments that match those in the ``is`` expression.

To simplify access to named parameters, the usage of the new trait ``getNamedParameters`` is recommended (see [Supporting Syntax](#supporting-syntax) for further details). Parameter names can then be suppplied to the ``getMember`` trait to obtain the corresponding argument.

It is possible fetch a function parameter's storage class, type, and name, but not a template parameter's, via the ``__parameters`` specialization of the ``is`` expression. It would make a good candidate for obtaining named parameters, but a proposal to change the return value to accomodate extra information would be a breaking change. Instead, the new trait ``isNamedParameter`` is proposed as a shortcut to detect if a given parameter is a named parameter.

A named parameter may appear in the parameter list after a variadic parameter. A named argument terminates the variadic parameter's argument list:

```d
void func(int[] args..., @named bool flag);

func(1, 2, flag: false); // ok
func(1, 2, flag: true, 3); // error: unexpected token '3' expecting ')'

template Foo(T, U... @named V) {}

Foo!(int, float, string, V: bool); // ok
Foo!(int, float, V: bool, string); // error: unexpected token 'string' expecting ')'
```

### Resolution

To resolve a named argument against named parameters, resolution for both function and template instantiations are amended. The below process is appended to each resolution algorithm as appropriate.

- For each named argument:
  - If the argument name has already been processed in this argument list, error.
  - If the argument name is found in the named parameter list (sublist of all parameters):
    - Perform positional argument checks (see [Reordering](#reordering)).
    - Confirm that the argument can be passed to the parameter, if not error.
  - Else:
    - Error.
- For each named parameter:
  - If there is no default value or corresponding named argument, error.

The existing resolution algorithms remain unchanged but may require refactoring to ignore named arguments.

#### Reordering

Argument reordering is a defining feature of named arguments. In the context of discussing potential implementations in D, forum conversations (including reviews of the first draft of this DIP) have led to different suggestions, including full, partial, and no reordering. The implementation of named arguments proposed in [DIP 1019] opts for the latter option. This DIP proposes a partial reordering scheme.

The following rules shall govern the reordering of named arguments:

* named arguments at the end of an argument list may appear in any order
    ```d
    void myFunction(int x, @named int y, @named int z, int w);

    myFunction(0, 3, z: 2, y: 1); // Success
    myFunction(0, z: 2, y: 1, 3); // Failure: positional argument at end
    myFunction(0, y: 1, z: 2, 3); // Success: matches parameter list order
    ```
* named arguments may be interleaved with positional arguments out of sequence, but if there are more than one they must match the order in which the named parameters are declared
    ```d
    void myFunction(int x, @named int y, @named int z, int w);

    myFunction(y: 1, 0, z: 2, 3); // Success
    myFunction(y: 1, z: 2, 0, 3); // Success
    myFunction(z: 2, 0, y: 1, 3); // Failure: y before z in parameter list
    myFunction(z: 2, y: 1, 0, 3); // Failure: y before z in parameter list
    myFunction(0, z: 2, y: 1, 3); // Failure: y before z in parameter list
    ```
The above is added to the resolution algorithm as the following semantic:

* If a positional argument appears after a named argument
    - If the named argument follows another and the preceding named argument appears after it in the parameter list, error

This revised semantic may serve as a transitional stage between partial reordering and a potential implementation of full reordering depending upon the experience gained.

### Use Cases

#### Express a parameter's intention
The declaration of ``std.string.indexOf`` includes one parameter, ``cs`` of the type ``CaseSensitive``, an alias to ``std.typecons.Flag!"caseSensitive".Flag``.

```d
ptrdiff_t indexOf(Range)(Range s, dchar c, CaseSensitive cs = Yes.caseSensitive)
```

The ``Flag`` template was added to the standard library in order to replace Boolean arguments of ``true`` and ``false`` with an argument that is more descriptive. In order to avoid requiring callers to use the full template instantiation on every call, the common pattern is for the implementer to declare an alias such as ``CaseSensitive``. Named parameters can meet the same use case with the same effect, but without requiring the additional alias and without a template instantiation:

```d
ptrdiff_t indexOf(C)(scope const(C)[] s, dchar c, size_t startIdx,
  @named bool caseSensitive = true)
  if (isSomeChar!C);
```

By applying ``@named`` to the parameter and giving it a more descriptive name than ``cs``, the type of the parameter need not be changed from ``bool``. The desired name remains colocated in the function declaration rather than in an alias somewhere external to the function.

This also has an impact on documentation. An alias and a function must be documented separately and they appear as separate listings in the generated documentation. Named parameters require only one Ddoc comment and the parameter name appears in the documentation for the function.

#### Make template parameters available for introspection
The following example demonstrates a pattern that is common in D code bases that make heavy use of meta-programming:

```d
struct Foo(T_) {
  alias T = T_;
}
```

When one desires to perform introspection on template parameters outside of the template declaration, one must declare an alias to the parameter inside the template declaration so that it may be accessed, in this case, as ``Foo.T``. This is because template parameters are not accessible via introspection outside of the template declaration.

A simplified real-world example that originates from IRC user AsumFace is shown below.

```d
import std.traits : isInstanceOf;

struct BasicPriorityQueue(T, ulong maxPriority_)
{
    alias maxPriority = maxPriority_;
    T[][maxPriority + 1] queue;

    void insert(A)(A arg)
        if (isInstanceOf!(PriorityItemData, A) && A.priority <= maxPriority)
    {

        queue[A.priority] ~= arg.payload;
    }
}

auto PriorityItem(ulong priority, T)(T payload)
{
    return PriorityItemData!(priority, T)(payload);
}

private struct PriorityItemData(ulong priority_, T)
{
    alias priority = priority_;
    T payload;
}
```

One should not be required to implement aliases for every template parameter one would like to make available for external introspection. Moreover, Ddoc comments will cause the aliased name to appear separately from the parameter name.

This document proposes that template parameters marked with ``@named`` be made accessible for introspection outside of the template declaration. The following example reimplements the above using named parameters as proposed:

```d
import std.traits : isInstanceOf;

struct BasicPriorityQueue(T, @named ulong maxPriority)
{
    T[][maxPriority + 1] queue;

    void insert(A)(A arg)
        if (isInstanceOf!(PriorityItemData, A) && A.priority <= maxPriority)
    {

        queue[A.priority] ~= arg.payload;
    }
}

auto PriorityItem(ulong priority, T)(T payload)
{
    return PriorityItemData!(T, priority: priority)(payload);
}

private struct PriorityItemData(T, @named ulong priority)
{
    T payload;
}
```

Once again, the named parameters not only eliminate the aliases, they clarify the documentation so that one need not search for the "actual" parameter name outside of the parameter list.

#### User-friendly interface
To construct an instance of ``core.time : Duration``, one generally uses the template ``dur``. This requires instantiating the template with the time unit expressed as a string, a task with which IDE autocompletion does not help. One must remember to use ``"seconds"`` rather than ``"secs"`` but ``"msecs"`` instead of ``"milliseconds"``. Strings are also vulnerable to typos that cause the program to fail to behave as expected, cannot be detected at compile-time, and can be difficult to detect during debugging.

Named parameters as proposed in this document can provide an alternative, more user-friendly interface to templated functions where all options are known at compile time:

```d
import core.time;
import std.typecons;

Duration dur(Nullable!Duration base = Nullable!Duration(),
              @named long hours = 0,
              @named long minutes = 0,
              @named long seconds = 0) {
    return base.get(dur!"hnsecs"(0)) + hours.dur!"hours" + minutes.dur!"minutes" + seconds.dur!"seconds";
}

writeln(dur(hours: 1,  minutes: 2, seconds: 3)); // 1 hour, 2 minutes, and 3 secs
```

#### Numerous arguments
Consider a function with a long parameter list, such as this C-style image blitter:

```d
void blit(Image* src, int sx, int sy, uint sw, uint sh,
          Image* dst, int dx, int dy, uint dw, uint dh)
```

The user must remember that the source image parameters come before the destination, position before dimensions, x before y, and width before height. With named parameters and a consistent naming convention, that is no longer necessary.

```d
void blit(@named Image* src, @named int sx, @named int sy,
          @named uint sw, @named uint sh, @named Image* dst,
          @named int dx, @named int dy, @named uint dw, @named uint dh) {}

blit(source: sprite, sx: 0, sy: 0, sw: 256, sh: 256,
     destination: canvas, dx: 100, dy: 100, dw: 256, dh: 256);
```

The counter to this example is that the number of parameters can be reduced with a little thought. Consider a more D-like version of the blitter:

```d
void blit(Image source, Image dest, Rect sourceRect, Rect destRect)
```

This was the signature of a function from a real project. Although they are hidden inside the ``Image`` and ``Rect`` types, the function conceptually has the same number of parameters as the C version above. For example, each ``Rect`` instance has members corresponding to the position and dimension parameters. If one constructs the ``Rect`` instances in place, one must still remember the order of the constructor arguments:

```d
blit(sourceImage, destImage, Rect(0, 0, 256, 256), Rect(100, 100, 256, 256));
```

Named parameters can be employed here with the same effect as above. However, with a little consideration, the number of arguments in this real-world scenario was decreased by reducing the amount of work the function had to perform and refactoring the API. The ``Image`` type was enhanced to be sliceable, making the additional ``Rect`` parameters redundant.

```d
blit(sourceImage[offsetX .. width, offsetY .. height], destImage[offsetDX .. dWidth, offsetDY .. dheight]);
```

This approach allows the use of descriptive variable names with the same effect as named parameters.

The ``Image`` example was a specific case in which an alternative solution was possible. Alternatives are not always possible. One does not always have full control over the APIs one uses, in which case refactoring the API is not always possible. Implementing a wrapper function in that case, one with descriptive named parameters, is always an option under this proposal.

## Breaking Changes and Deprecations

No breaking changes are expected except in the case that any codebase declares an existing UDA called `named`.

## Reference
There have been many conversations in the D forums attempting to promote named arguments. For example, [1](https://forum.dlang.org/post/khcalesvxwdaqnzaqotb@forum.dlang.org), [2](https://forum.dlang.org/post/n8024o$dlj$1@digitalmars.com) and [3](https://forum.dlang.org/post/ikhjf7$1tga$2@digitalmars.com).

Multiple library solutions have been attempted [1](https://forum.dlang.org/post/awjuoemsnmxbfgzhgkgx@forum.dlang.org), [2](https://github.com/CyberShadow/ae/blob/master/utils/meta/args.d) and [3](https://forum.dlang.org/post/wtccivdgrgteyinqwtdr@forum.dlang.org). Each work for the author's purpose but they have been known to be less than desirable to work with e.g. [1](https://forum.dlang.org/post/xwghendahfjgceikuxvh@forum.dlang.org), [2](https://forum.dlang.org/post/ohrilhjbhddjkkqznlsn@forum.dlang.org) and [3](https://forum.dlang.org/post/n837bu$vam$5@digitalmars.com). However, because all of these are library-based solutions, they cannot solve the internal versus public API friction that this DIP offers for named parameters.

[DIP 88] was drafted but was never accepted nor implemented. Further work has been done by Neia Neutuladh, but it has not been made public. [DIP 1019], "Named Arguments Lite", entered the review queue prior to this one, but its proposal for named arguments is more limited.

[DIP 1019]: https://github.com/dlang/DIPs/blob/3bc3469a841b87517a610f696689c8771e74d9e5/DIPs/DIP1019.md
[DIP 88]: https://wiki.dlang.org/DIP88

## Copyright & License

Copyright (c) 2019 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

### Community Review Round 1

[Reviewed Version](https://github.com/dlang/DIPs/blob/39dbbbe5e4618abd4c4b41eb0edd16547858ddf5/DIPs/DIP1020.md)

[Discussion](https://forum.dlang.org/post/q7qc3j$qgh$1@digitalmars.com)

One frequent complaint was the use of angle brackets to denote named parameters, either for being unsightly or based on the potential for parsing ambiguities as seen with C++ templates. The DIP does not consider alternative syntaxes. It refers to the alternative proposal in DIP 1019 but does not make a comparison.

Another common theme was that the proposal lacks information: the "Use Cases" section needs more detail, the "Rationale" section needs more detail, there is no explanation of how the DIP is intended to aid with "the implementation tooling" as mentioned in the rationale, access to named template parameters via template member syntax needs justification, the "Future Proposals" section is weak.

The lack of support for argument reordering was brought up multiple times as a flaw, though some argued against it.

One of the language maintainers put forth a simpler, alternative proposal for named arguments.

### Community Review Round 2

[Reviewed Version](https://github.com/dlang/DIPs/blob/c723d8f4e3ac2d5bcaf8cdff87e1507f09669ca6/DIPs/DIP1020.md)

[Discussion](https://forum.dlang.org/post/guvpjsspfzaruimrmfxb@forum.dlang.org)

The rationale for the `@named` attribute was questioned, with C# cited as an example of a language allowing named arguments without parameter annotations that does not suffer from breakage when parameter names change. The point was made named arguments would be unusable with any existing library until it was updated to support them. Others suggested alternatives to `@named`, such as `public`.

There was some discussion about whether the feature should affect name mangling and overload resoltion.

There were requests that the DIP provide a comparison to the alternative proposal put forth by one of the language maintainers in the prior review. The language maintainer said the DIP should provide a rationale for why argument reordering is undesirable and should justify the difference from struct literal syntax. The DIP author agreed.

### Addendum
This DIP was postponed by the author and subsquently superseded by [the acceptance of DIP 1030](../accepted/DIP1030.md).

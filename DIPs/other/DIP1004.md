# Inherited Constructors

| Section         | Value                                                      |
|-----------------|------------------------------------------------------------|
| DIP:            | 1004                                                       |
| Author:         | Andrej MitroviÄ‡                                            |
| Review Count:   | 1 [Most Recent]                                            |
| Implementation: |                                                            |
| Status:         | Abandoned                                                  |

[Most Recent]: https://github.com/dlang/DIPs/blob/e94c8ae3af08aaabc936e0306894e7fae06e9b71/DIPs/DIP1004.md

## Table of Contents

- [Abstract](#abstract)
- [Rationale](#rationale)
- [Description](#description)
  - [Explanation of the problem](#explanation-of-the-problem)
  - [Existing solutions](#existing-solutions)
- [Proposed changes](#proposed-changes)
  - [Implicit constructor inheritance for derived classes with no constructors](#implicit-constructor-inheritance-for-derived-classes-with-no-constructors)
  - [Syntax for inheriting base class constructors for derived classes](#syntax-for-inheriting-base-class-constructors-for-derived-classes)
    - [Syntax variations](#syntax-variations)
  - [Selective disabling of inherited constructors](#selective-disabling-of-inherited-constructors)
    - [Applicability for this proposal](#applicability-for-this-proposal)
- [Examples from existing projects](#examples-from-existing-projects)
  - [Sociomantic tango/ocean](#sociomantic-tangoocean)
  - [Phobos](#phobos)
- [Breaking changes](#breaking-changes)
- [Links & Previous discussions](#links-&-previous-discussions)

## Abstract

A derived class is currently instantiable by client-code using a base class
constructor only if the base class has a default constructor and the derived class does not define any constructors of its own.

If the implementer of the derived class wants to allow construction of the
derived class by using any of the base class constructors, then this support
must be added manually by writing forwarding constructors.

This DIP attempts to alleviate the problem by introducing three features:

- [Implicit constructor inheritance for derived classes with no constructors](#implicit-constructor-inheritance-for-derived-classes-with-no-constructors)
- [Syntax for inheriting base class constructors for derived classes](#syntax-for-inheriting-base-class-constructors-for-derived-classes)
- [Selective disabling of inherited constructors](#selective-disabling-of-inherited-constructors)

## Rationale

The process of manually writing forwarding constructors is tedious and
error-prone. It's possible to automate it using mixins, but that is not a
reliable solution. Drawbacks are explained in more details in a[dedicated section](#existing-solutions).

## Description

### Explanation of the problem

Currently a derived class is constructible by client code via its base class
constructor only if both of the following are true:

- The derived class does not define any of its own constructors
- The base class constructor has a default constructor

Example:

```D
class Base
{
    this ( ) { }
}

class Derived : Base { }

void main ( )
{
    auto d = new Derived;  // OK. Derived doesn't define custom constructors
}
```

However, if the base class constructor is a non-default constructor it won't
be used:

```D
class Base
{
    this ( int ) { }
}

class Derived : Base { }

void main ( )
{
    // Error: class Derived cannot implicitly generate a default ctor when
    // base class Base is missing a default ctor
    auto d = new Derived(42);
}
```

Even if the base class has a default constructor but derived class defines one of its own, the base constructor also can't be used:

```D
class Base
{
    this ( ) { }
}

class Derived : Base
{
    this (int, int) { }
}

void main ( )
{
    // Error: constructor Derived.this (int _param_0, int _param_1) is not
    // callable using argument types ()
    auto d = new Derived();
}
```

The rationale for the latter limitation: if the `Derived` object was
constructible by calling only the `Base` class constructor then the `Derived`
object itself could be left in an *unexpected* state. This is because the
`Derived` constructors could be skipped and never called and potentially
leaving `Derived`'s own internal fields in an unexpected state.

### Existing solutions

Currently, if the author of the `Derived` class wants to allow construction via
the base class constructor then this support must be added explicitly by
writing forwarding constructors:

```D
class Base
{
    this ( int ) { }
}

class Derived : Base
{
    this ( )
    {
        super(42);
    }

    this ( int x )
    {
        super(x);
    }
}

void main ( )
{
    auto d1 = new Derived();  // ok
    auto d2 = new Derived(43);  // ok
}
```

An example library solution for inheriting constructors via mixins is provided
[here](https://gist.github.com/AndrejMitrovic/72a08aa2c078767ea4c35eb1b0560c8d).

There are a number of drawbacks with the above implementation:

- It relies on string mixins to generate the code, which can slow down
  compilation speed.
- It does not handle default values.
- It does not have the ability to selectively disable inheriting specific
  constructors. The API could be expanded to support this, however it would
  make the API awkward to use.
- Used self-reflection can potentially create semantic analysis cycles and
  thus is very fragile against changes in compiler internals.

## Proposed changes

This document proposes two independent language changes. Combined, they will
fix all of the aforementioned issues in a way that integrates naturally into the existing language definition.

### Implicit constructor inheritance for derived classes with no constructors

When doing semantic analysis of a class, if that class does not define any
constructors of its own then all constructors of its direct base are implicitly
inherited.

Before:

```D
class ParseException : Exception
{
    // Error: class ParseException cannot implicitly generate a default ctor
    // when base class object.Exception is missing a default ctor
}

void parseArgs ( string[] args )
{
    if (args < 2)
    {
        throw new ParseException("Expected at least one argument");
    }
}
```

After:

```D
class ParseException : Exception
{
    // Compiles, inherits constructors defined in Exception class
}

void parseArgs ( string[] args )
{
    if (args < 2)
    {
        throw new ParseException("Expected at least one argument");
    }
}
```

```D
class ParseException : Exception
{
    // Explicit constructor definition, prevents implicit inheriting
    // of constrcutors defined in Explicit class:

    this ( ) { super(""); }
}

void parseArgs ( string[] args )
{
    if (args < 2)
    {
        // Error: constructor ParseException.this () is not callable using
        // argument types (string)
        throw new ParseException("Expected at least one argument");
    }
}
```

**Rationale**: If a derived class does not define any of its own constructors
then there is no danger of leaving the object in an unexpected state.

Side-note: Forwarding constructors are most prominent in exception class
hierarchies.

### Syntax for inheriting base class constructors for derived classes

Uses existing `alias <symbol> this` syntax but allows the symbol to be the `super.this` special sequence. If present, it is interpreted as an explicit request to inherit all direct base class constructors in a similar way as
[the first proposed change](#implicit-constructor-inheritance-for-derived-classes-with-no-constructors)
does implicitly.

Before:

```D
enum ErrorCode
{
    FileNotFound
}

class FileException : Exception
{
    /// introduce overload which takes an error code
    this ( ErrorCode error_code, string file = __FILE__, string line = __LINE__ )
    {
        super(error_code.to!string, file, line);
    }
}

void main ( string[] args )
{
    // Error: constructor FileException.this () is not callable using
    // argument types (string)
    throw new FileException("Something went wrong");

    // OK
    throw new FileException(ErrorCode.FileNotFound);
}
```

After:

```D
enum ErrorCode
{
    FileNotFound
}

class FileException : Exception
{
    /// introduce overload which takes an error code
    this ( ErrorCode error_code, string file = __FILE__, string line = __LINE__ )
    {
        super(error_code.to!string, file, line);
    }

    /// inherit base class constructors
    alias super.this this;
}

void main ( string[] args )
{
    // using Exception's constructor
    throw new FileException("Something went wrong");

    // using FileException's constructor
    throw new FileException(ErrorCode.FileNotFound);
}
```

This feature is similar to D's existing feature for re-introducing base class
methods to allow overloading of a base class method:

```D
class Base
{
    int sum ( int x, int y ) { return x + y; }
}

class Derived : Base
{
    // re-introduces base class method to allow overloading
    alias super.sum sum;

    // overload of the base class sum method
    float sum ( float x, float y ) { return x + y; }
}

void main ( string[] args )
{
    auto d = new Derived;
    d.sum(int(1), int(2));
    d.sum(float(1), float(2));
}
```

#### Syntax variations

This document is written with the `alias super.this this` syntax in mind, but it is not crucial for the purpose of the proposal. There are at least several
possible syntaxes which can be used for this feature:

```D
class FileException : Exception
{
    /// option A: simple syntax which references the ctor via the keyword 'this'
    alias super.this this;

    /// option B: using parentheses to make it explicit we're refering to ctors
    alias super.this() this();

    /// option C: the most simplistic syntax, although it may be confused with
    /// the syntax for subtyping
    alias super this;
```

### Selective disabling of inherited constructors

Extends the `@disable <symbol>` syntax to optionally allow a specific parameter
type list, allowing the disabling of one of many overloads of the same method. If a parameter type list is provided via the `@disable <symbol>(<comma-separated type list>)` syntax, only the matching overload is disabled.

#### Applicability for this proposal

Since the proposed feature for explicitly inheriting base class constructors
actually inherits **all** base-class constructors, there may be a need to allow
disabling the inheritance of specific constructors. For example:

```D
class Base
{
    this ( int x, int y ) { }
    this ( string x, string y ) { }
}

class Derived : Base
{
    alias super.this this;

    @disable this(int, int);  // disable this overload of the inherited constructor
}

void main ( )
{
    new Derived("foo", "bar");  // ok
    new Derived(1, 2);  // error: constructor is disabled
}
```

## Examples from existing projects

The most common example in library code is an exception class hierarchy.
Typically each new inherited class has to define forwading constructors in
order to make it usable.

### Sociomantic tango/ocean

An example of an exception class hierarchy can be found
[here](https://github.com/sociomantic-tsunami/ocean/blob/6500d67e630de1d05adc510e1572bee26fe3985c/src/ocean/core/Exception_tango.d).

Note that this
[wrapped constructor](https://github.com/sociomantic-tsunami/ocean/blob/6500d67e630de1d05adc510e1572bee26fe3985c/src/ocean/core/Exception_tango.d#L91-L97)
definition is actually incorrect and ignores file/line parameters, most likely
because of developers' reluctance to write required boilerplate:

```D
class PlatformException : Exception
{
    this( istring msg )
    {
        super( msg );
    }
}
```

With this DIP features, it would look like this:

```D
class PlatformException : Exception { }
```

That is both shorter and more correct as it is guaranteed to use the same
constructor definition. It also has the benefit of being more future-proof
against base constructor definition changes.

### Phobos

The D standard library also defines a helper mixin to make it easier to define
custom exception classes
[here](https://github.com/dlang/phobos/blob/bf61ad682f3f9c35a16c79180941ffd902ab9758/std/exception.d#L2159), indicating it is a common task.

However, this mixin is not used even within Phobos itself and a quick search
shows many exception classes with incorrect constructor definitions. This is
an incomplete list of such cases:

- [std.concurrency](https://github.com/dlang/phobos/fa54b85dd97598f3cf775a1b36bc0d8944f45f18/master/std/concurrency.d#L224)
- [std.json](https://github.com/dlang/phobos/blob/fa54b85dd97598f3cf775a1b36bc0d8944f45f18/std/json.d#L1295)
- [std.zip](https://github.com/dlang/phobos/blob/fa54b85dd97598f3cf775a1b36bc0d8944f45f18/std/zip.d#L78)
- [std.stdio](https://github.com/dlang/phobos/blob/fa54b85dd97598f3cf775a1b36bc0d8944f45f18/std/stdio.d#L4266)

Examples of such wrong code present even in the standard library prove that it is a very easy mistake to make. This proposal helps to eliminate such cases by
making the right way easy and  the wrong one to require additional manual effort.

## Breaking changes

The vast majority of code should not be affected by the changes in this proposal.

Code which uses introspection to check whether a class is instantiable with a
list of arguments would be the most affected by this change.  For example, an
`is(typeof( new Derived(...) ))` check may currently be false, but would change
to true if constructors were implicitly inherited as per
[feature #1](#implicit-constructor-inheritance-for-derived-classes-with-no-constructors)

## Links & Previous discussions

* [Issue 9066: Add constructor inheritance feature](https://issues.dlang.org/show_bug.cgi?id=9066)
* [Previous discussion about constructor inheritance in D](http://forum.dlang.org/post/f3pisd$4vi$3@digitalmars.com)
* [C++11: What is constructor inheritance?](http://stackoverflow.com/a/9979249/279684)
* [C++11: Draft 3337 - Page 269 - Constructor Inheritance (PDF warning)](http://www.open-std.org/jtc1/sc22/wg21/docs/papers/2012/n3337.pdf)
* [C++11: Object construction improvements](https://en.wikipedia.org/wiki/C%2B%2B11#Object_construction_improvement)

## Copyright & License

Copyright (c) 2016 by the D Language Foundation

Licensed under [Creative Commons Zero
1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

[Preliminary Review Round 1](http://forum.dlang.org/post/bsasudihqubudwgprpph@forum.dlang.org)

## Addendum
This DIP never completed the review process. It was marked as "Abandonded" after repeated attempts to communicate with the author failed. The review process may be resumed by anyone willing to take over the DIP as the point of contact. The DIP Manager will provide any such person guidance on how to proceed.
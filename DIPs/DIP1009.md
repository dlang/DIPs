# Improve Contract Syntax

| Field             | Value                                                           |
|-------------------|-----------------------------------------------------------------|
| DIP:              | 1009                                                            |
| Review Count:     | 1 [Most Recent]                                                 |
| Author:           | Zach Tollen - reachzach@gmail.com                               |
| Draft 2 Ideas by: | H.S. Teoh, Moritz Maxeiner, Solomon E, Zach Tollen, Timon Gehr  |
| Implementation:   | [Timon Gehr]                                                    |
| Status:           | Post-Preliminary Round 1                                        |

[Most Recent]: https://github.com/dlang/DIPs/blob/d2dc77802c74378cf4545069eced21f85fbf893f/DIPs/DIP1009.md

[Timon Gehr]: https://github.com/dlang/dmd/compare/master...tgehr:contract-syntax

## Abstract

D has already made a significant commitment to the theory of [Design by Contract](http://wiki.c2.com/?DesignByContract), by means of its existing `in`, `out`, and `invariant` constructs. But limitations remain to their practical usability. This DIP intends to redress the syntax aspect of those limitations. A new syntax for `in`, `out`, and `invariant` contracts is proposed, making them much easier to read and write.

### Links

[DIP1003 thread which initiated this DIP](http://forum.dlang.org/post/cklhgfbnpajbeefmwjrf@forum.dlang.org)

## Rationale

D's existing `in` and `out` contract syntaxes are hard to use for three reasons. The first is that one must create a block statement for any contract, even a simple one-liner. The largest share of contracts will be one line long. In order to keep this manageable for codebases that use them, a strict formatting style must be used. For example, the [SDC](https://github.com/SDC-Developers/SDC/) codebase, writes contracts [like this:](https://github.com/SDC-Developers/SDC/blob/master/src/d/parser/expression.d#L1059)
```d
ulong strToDecInt(string s) in {
    assert(s.length > 0, "s must not be empty");
} body {
    ulong ret = 0;
    ...
}  
```
The same contract in any other formatting style, including the standard style of DMD and Phobos, would cause them to take up so much space that they would not be worthwhile:
```d
ulong strToDecInt(string s)
in
{
    assert(s.length > 0, "s must not be empty");
}
body
{
    ulong ret = 0;
}
```
The second reason contracts are hard to use is that they require the use of the keyword `do` (or `body`, see [DIP1003](https://github.com/dlang/DIPs/blob/master/DIPs/DIP1003.md)) before the function body. Any way of writing them that could eliminate this need would improve things.

Finally, contracts in D require explicitly writing `assert`, when the vast majority of programmers can be expected to use D's existing `assert` functionality. It is unnecessary to force programmers to write `assert` when it can be assumed. By keeping all existing syntaxes, programmers who need to write something other than `assert` will still have that option available.

All these limitations can be removed by adding a new syntax. History shows that a small change in syntax can dramatically change programmers' willingness to use language features. An increase in usage of D's contracts can be expected.

Furthermore, because the existing `invariant` grammar is so similar to `in` and `out` contract grammar, any improvements to `in` and `out` contracts can be transferred easily to `invariant`s as well. Consistency across language constructs is desirable.

## Description

Allow `in`, `out`, and `invariant` contracts to be written as parenthesized expressions. Allow multiple expressions of this kind. Create a new grammatical construct called ContractParameters, which copies and replaces the parameters to `assert` expressions. Use the new construct for `in`, `out`, `assert`, and `invariant` expressions alike. In the case of `out` expressions, require a semicolon separating an optional return identifier from the ContractParameters, which come afterward. `invariant` expressions require a semicolon terminator. The keyword `do` or `body` is not required after `in` and `out` contracts that use the new syntax. For example:

```d
int fun(ref int a, int b)
   in(a > 0)
   in(b >= 0, "b cannot be negative!")
   out(r; r > 0, "return must be positive")
   out(; a !=0)
{
   // function body
}

class C {
   int data;
   invariant(data != 0, "data cannot be 0");
}
```
In existing D, the above contracts would have to be written as (and would in most cases lower to):
```d
int fun(ref int a, int b)
in {
   assert(a > 0);
   assert(b >= 0, "b cannot be negative!");
}
out(r) {
   assert(r > 0, "return must be positive");
   assert(a !=0);
}
do {
   // function body
}

class C {
   int data;
   invariant {
      assert(data != 0, "data cannot be 0");
   }
}
```

Note that while the new `in`, `out`, and `invariant` expressions use the same grammar as `assert`, and the ready-made `assert` checking logic can be used to implement them, this DIP does not declare that that logic must be used. If different hooks for the new expressions are deemed valuable, this DIP does not prohibit them.

## Analysis

The benefits of the new syntax are rather obvious. By removing the syntactic noise from the grammar, an increase in the usage of contracts can be expected.

The drawback of this conciseness is that a usability gap may open up between those who find the built-in `assert` functionality adequate to their needs, and those who, for whatever reason, are forced to use some other system of checking. The existing `in` and `out` contract syntax is still available for use by the latter. But the language now more strongly promotes using existing `assert` functionality where possible. The incentive is increased, therefore, to make `assert` serve as many people as possible, however that is achieved.

### Analysis of the new `out` syntax

The most desirable syntax for the new `out` expression, i.e. a single parenthesized set of `assert` arguments,  is ambiguous with the existing `out` contract grammar. The presence of nothing but a single identifier inside the parentheses could be interpreted as either syntax, and thus can't be recommended. All of the proposed alternatives were imperfect in one way or another. One such alternative is to require two sets of parentheses, which resembles template function declarations and the existing `out` contract syntax, e.g.:
```d
int fun(ref int i)
  out(r)(r > 0)
  out()(i > 0)
{
  ...
}
```
However, the semicolon syntax is recommended as the "least imperfect" option, because:
* it resembles `for` and `foreach` loops, and the new `out` expression resembles `foreach` semantics slightly more than it resembles any other semantics
* it requires only one set of parentheses, which is consistent with the other new contracts
* it is the most concise of all the proposals, requiring only an additional `;` to achieve clear parsing

Note that existing `foreach` statements do not allow omitting the initial identifier, whereas the new `out` syntax does allow this. If total consistency is desired, it seems preferable to change `foreach` to allow omitting the identifier, rather than forcing both syntaxes to define one that is never used.

## Grammar
The grammar for `assert` parameters is redefined as ContractParameters, which is used in all the new constructs. Existing `in`, and `out` statements remain as they are. `invariant` statements are amended with the new variation. New grammar follows:
```
ContractParameters:
    AssignExpression ,opt
    AssignExpression , AssignExpression ,opt

AssertExpression(redefined):
    assert ( ContractParameters )

InExpression:
    in ( ContractParameters )

OutExpression:
    out ( ; ContractParameters )
    out ( Identifier ; ContractParameters )

Invariant(redefined):
    invariant ( ) BlockStatement
    invariant BlockStatement
    invariant ( ContractParameters ) ;

FuncDeclaratorSuffix(redefined):
    Parameters MemberFunctionAttributes,opt ContractExpressions,opt
    TemplateParameters Parameters MemberFunctionAttributes,opt Constraint,opt ContractExpressions,opt

ContractExpressions:
    FunctionContract
    FunctionContract ContractExpressions

FunctionContract:
    InExpression
    OutExpression
    InStatement
    OutStatement
```
## Code Breakage

None of the new syntax is currently compilable, so no code is expected to break.

## Copyright & License

Copyright (c) 2017 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

[Preliminary Review Round 1](http://forum.dlang.org/post/gjtsfysvtyxcfcmuutez@forum.dlang.org)

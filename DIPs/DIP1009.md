# Improve Contract Syntax

| Field             | Value                                                           |
|-------------------|-----------------------------------------------------------------|
| DIP:              | 1009                                                            |
| Review Count:     | 2 [Most Recent]                                                 |
| Author:           | Zach Tollen(reachzach@gmail.com)                                |
| Implementation:   | [Timon Gehr]                                                    |
| Status:           | Formal Review                                                   |

[Most Recent]: https://github.com/dlang/DIPs/blob/5ac6323098035773fe8ca18362832e0162344b7f/DIPs/DIP1009.md

[Timon Gehr]: https://github.com/dlang/dmd/compare/master...tgehr:contract-syntax

## Abstract

The D language supports [Contract Programming](https://dlang.org/spec/contracts.html), by means of the `in`, `out`, and `invariant` constructs; however, as is outlined in the [Rationale section](#rationale), the current contract syntax is quite "syntactically heavy" and can make function definitions appear visually cluttered. This DIP is intended to address this issue by proposing a syntax which makes `in`, `out`, and `invariant` contracts shorter and less heavy.

### Links
[DIP1003 discussion thread](http://forum.dlang.org/post/cklhgfbnpajbeefmwjrf@forum.dlang.org), which was the impetus for this DIP.

## Rationale

D's existing `in` and `out` contract syntaxes are inconvenient to use for the following reasons:

1. One must create a block statement for each contract. Many contracts are one line long, requiring a strict formatting style for consistency and readability. For example, contracts in the [SDC](https://github.com/SDC-Developers/SDC/) codebase [are formatted](https://github.com/SDC-Developers/SDC/blob/master/src/d/parser/expression.d#L1059) as follows: 
```d
ulong strToDecInt(string s) in {
    assert(s.length > 0, "s must not be empty");
} body {
    ulong ret = 0;
    ...
}  
```
The same contract would occupy significantly more vertical space in other codebases (such as dmd or Phobos):
```d
ulong strToDecInt(string s)
in
{
    assert(s.length > 0, "s must not be empty");
}
body
{
    ulong ret = 0;
}
```

2. Contracts require the use of the keyword `do` (or `body`, see [DIP1003](https://github.com/dlang/DIPs/blob/master/DIPs/DIP1003.md)) before the function body. Any syntax that renders this requirement optional or removes it will shorten contract definitions.

3. Generally, `assert` statements are used to perform checks within `in` and `out` contract blocks. Currently, the language requires these statements to be written explicitly. This is tedious and, for the common case, unnecessary. The presence of a single Boolean expression in an `in`, `out`, or `invariant` block is sufficient to assume an implicit assert. Programmers who prefer an alternative method of verifying contract conditions can still use the "old" contract syntax.

## Description

The language grammar will be modified to introduce new, optional syntax for `in` and `out` contracts, called _contract expressions_. These expressions will make contract assertions more concise and readable by eliminating the need for braces and `assert` statements when declaring `in` and `out` contracts, as well as `invariant` contracts.

The syntax is designed to mimic that of a call to `assert`, with two possible special forms proposed for `out` contracts that require access to the function's return value. As is described in the [Analysis of the new `out` syntax section](#analysis-of-the-new-out-syntax), option 2 is recommended for these special `out` contract forms.

Multiple `in` and `out` contract expressions are permitted for a single function. Contract expressions do not require terminating semicolons, and the keyword indicating the beginning of the function body is optional; the body's opening brace is sufficient.

Example:
```d
int fun(ref int a, int b)
   in(a > 0)
   in(b >= 0, "b cannot be negative!")
   out(r; r > 0, "return must be positive")
   out(; a != 0)
{
   // function body
}
```

Contracts using the old contract syntax may still be present, but must follow the contract expressions. In this case, the keyword indicating the beginning of the function body is required.

Example:
```d
int fun(ref int a, int b)
in(a > 0)
in {
      assert(b >= 0, "b cannot be negative!");
}
do {
   // function body
}
```

In the case of body-less virtual interface functions, a semicolon is required to terminate the function declaration. This differs from the old contract syntax, which does not require a terminating semicolon for body-less functions.

Example:
```d
interface I {
   int fun(int a) in(a > 0); 
}
```

This DIP also extends the proposed syntax to `invariant` contracts. Like the proposed function contract syntax, the proposed `invariant` contract syntax is designed to mimic a call to `assert`. These modified invariant contracts must be terminated with a semicolon.

Example:
```d
class C {
   int data;
   invariant(data != 0, "cannot be 0");
}
```

The proposed contract syntax will be lowered to the old syntax. For example, the above contracts written with the proposed syntax would be lowered as follows:
```d
int fun(ref int a, int b)
in {
   assert(a > 0);
   assert(b >= 0, "b cannot be negative!");
}
out(r) {
   assert(r > 0, "return must be positive");
   assert(a != 0);
}
do {
   // function body
}

interface I {
   int fun(int a)
   in {
      assert(a > 0);
   } 
}

class C {
   int data;
   invariant {
      assert(data != 0, "data cannot be 0");
   }
}
```

This DIP does not prohibit the use of validation constructs other than `assert` inside a contract, but is based on the assumption that `assert` is the preferred method. Replacing `assert` in the lowered code with an alternative is beyond the scope of this DIP.

## Analysis

By shortening the contract syntax in D, the author expects increased usage of function contracts and invariants in user code.

A drawback of the conciseness of the proposed syntax is that it requires the use of `assert` for verifying invariants and function pre and post conditions. If a user wishes to use some other method, such as `enforce` or their own function, they must instead use the more verbose syntax.

### Analysis of the new `out` syntax

The proposed new contract syntax is ambiguous with the old syntax in the case of an `out` contract expression containing a single identifier. `out(i)` could be parsed as either a contract expression asserting the truthiness of `i`, or the beginning of an `out` contract using the old contract syntax that introduces `i` as a parameter.

Example:
```d
int fun(ref int i)
  out(i != 0) // okay
  out(i) // whoops! ambiguous with existing contract syntax
{
   // out contract, not function body
}
```

The most straightforward solution is for the new grammar to forbid this case. Instead of `out(i)` the user will be required to disambiguate by writing `out(i != 0)` or `out(i !is null)`.

Example:
```d
int fun(ref int i)
  out(i != 0) // okay
  out(i) // error: single-identifier expressions in out contracts disallowed
{
   // function body
}
```

However, it is preferable to avoid this ambiguity altogether. Two possibilities for `out` contract syntax follow:

1. Use two sets of parentheses; one to introduce the contract parameter and one for the contract expression. This resembles the way the old `out` contract syntax declares the contract identifier, and also resembles the syntax of template function declarations.

Example:
```d
int fun(ref int i)
  out(r)(r > 0)
  out()(i > 0) // unambiguous with the current contract syntax
{
  // function body
}
```

2. Require the use of a semicolon to separate the contract parameter from the expression body. This resembles the syntax of `for` and `foreach` loops, and does not require multiple sets of parentheses, which users may find visually confusing. It is also more concise.

Example:
```d
int fun(ref int i)
  out(r; r > 0)
  out(; i > 0)
  out(; i)  // these are all unambiguous
{
  ...
}
```

Note that existing `foreach` statements do not allow omitting the initial identifier, whereas the proposed syntax does. If total consistency is desired, `out` contracts may be required to introduce a contract parameter.

## Grammar
The grammar for `assert` parameters is redefined as ContractParameters, which is used in all the new constructs. Existing `in`, and `out` statements remain as they are. `invariant` statements are amended with the new variation. The `foreach` version of the new syntax is described here. New grammar follows:
```
ContractParameters:
    AssignExpression ,opt
    AssignExpression , AssignExpression ,opt

AssertExpression(redefined):
    assert ( ContractParameters )

InExpression:
    in ( ContractParameters )

OutExpression:
    out ( ; ContractParameters )
    out ( Identifier ; ContractParameters )

Invariant(redefined):
    invariant ( ) BlockStatement
    invariant BlockStatement
    invariant ( ContractParameters ) ;

FuncDeclaratorSuffix(redefined):
    Parameters MemberFunctionAttributes,opt ContractExpressions,opt
    TemplateParameters Parameters MemberFunctionAttributes,opt Constraint,opt ContractExpressions,opt

ContractExpressions:
    ContractExpression
    ContractExpression ContractExpressions

ContractExpression:
    InExpression
    OutExpression
```
## Code Breakage

This DIP proposes new syntax and thus no code breakage is anticipated.

## Acknowledgements

This DIP was based upon an idea initially proposed by H.S. Teoh. It incoporates ideas from Moritz Maxeiner, Solomon E, Timon Gehr, and the DIP author, and was very helpfully edited by Mike Parker.

## Copyright & License

Copyright (c) 2017 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

[Preliminary Review Round 1](http://forum.dlang.org/post/gjtsfysvtyxcfcmuutez@forum.dlang.org)

[Preliminary Review Round 2](http://forum.dlang.org/thread/luhdbjnsmfomtgpydser@forum.dlang.org)

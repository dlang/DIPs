# Improve Contract Syntax

| Field             | Value                                                           |
|-------------------|-----------------------------------------------------------------|
| DIP:              | 1009                                                            |
| Review Count:     | 2 [Most Recent]                                                 |
| Authors:          | Zach Tollen(reachzach@gmail.com)                                |
| Implementation:   | [Timon Gehr]                                                    |
| Status:           | Formal Review                                                   |

[Most Recent]: https://github.com/dlang/DIPs/blob/5ac6323098035773fe8ca18362832e0162344b7f/DIPs/DIP1009.md

[Timon Gehr]: https://github.com/dlang/dmd/compare/master...tgehr:contract-syntax

## Abstract

D has already made a significant commitment to the theory of [Contract Programming](https://dlang.org/spec/contracts.html), by means of its existing `in`, `out`, and `invariant` constructs. But limitations remain to their usability, both in their syntax and in their implementation. This DIP addresses only the syntax aspect of those limitations, proposing a syntax which makes `in`, `out`, and `invariant` contracts much easier to read and write.

### Links
[DIP1003 discussion thread](http://forum.dlang.org/post/cklhgfbnpajbeefmwjrf@forum.dlang.org), which was the impetus for this DIP.

## Rationale

D's existing `in` and `out` contract syntaxes are hard to use for several reasons:

1. One must create a block statement for any contract. Many contracts will be one line long, requiring a strict formatting style for consistency and readability. For example, contracts in the [SDC](https://github.com/SDC-Developers/SDC/) codebase [are formatted](https://github.com/SDC-Developers/SDC/blob/master/src/d/parser/expression.d#L1059) as follows: 
```d
ulong strToDecInt(string s) in {
    assert(s.length > 0, "s must not be empty");
} body {
    ulong ret = 0;
    ...
}  
```
The same contract would occupy significantly more vertical space in other codebases (such as dmd or Phobos):
```d
ulong strToDecInt(string s)
in
{
    assert(s.length > 0, "s must not be empty");
}
body
{
    ulong ret = 0;
}
```

2. Contracts require the use of the keyword `do` (or `body`, see [DIP1003](https://github.com/dlang/DIPs/blob/master/DIPs/DIP1003.md)) before the function body. Any syntax that renders this requirement optional or obsolete would be an improvement.

3. `assert` statements, or something equivalent, are used to perform checks within `in` and `out` contract blocks. Currently, the language requires these statements to be written explicitly. This is tedious and, for the common case, unnecessary. The presence of a single Boolean expression in an `in` or `out` block should be sufficient to assume an implicit assert. For those programmers who require the use of other forms of checking, the existing syntax will remain unchanged. 

Furthermore, because the existing `invariant` grammar is so similar to `in` and `out` contract grammar, any improvements to `in` and `out` contracts can be transferred easily to invariants as well. Consistency across language constructs is desirable.

## Description

Modify the language grammar to introduce new, optional syntaxes for `in` and `out` contracts, called contract expressions, with the goal of making them more concise and readable by eliminating the need for braces and `assert` statements. The new expressions can be referred to by the new grammar symbols InExpression and OutExpression, respectively.

An InExpression is `in`, followed by parentheses containing a Boolean expression. This syntax exactly mimics that of a call to `assert`.

The syntax for OutExpression must be chosen from a few candidates (see Analysis). The recommended syntax is `out`, followed by parentheses containing, in the listed order: an optional return identifier, a mandatory semicolon (even when the return identifier is absent), and a Boolean expression as found in a call to `assert`.

Multiple `in` and `out` contract expressions are permitted in a single function. No semicolon is required to terminate a contract expression, and the keyword indicating the beginning of the function body is optional; the body's opening brace is sufficient. For example:
```d
int fun(ref int a, int b)
   in(a > 0)
   in(b >= 0, "b cannot be negative!")
   out(r; r > 0, "return must be positive")
   out(; a != 0)
{
   // function body
}
```
Ordinary bracketed `in` or `out` contracts can still be present, but must come after the contract expressions, and still require the `do` keyword:
```d
int fun(ref int a, int b)
in(a > 0)
in {
      assert(b >= 0, "b cannot be negative!");
}
do {
   // function body
}
```
In the case of body-less virtual interface functions, a semicolon is required if there is no bracketed contract:
```d
interface I {
   int fun(int a) in(a > 0); 
}
```
Also introduce a new type of `invariant` expression, terminated by a semicolon, with the grammar again mimicking an `assert` call:
```d
class C {
   int data;
   invariant(data != 0, "cannot be 0");
}
```
Contracts using the new syntax should be lowered by the compiler to the existing syntax, so that the above examples would be lowered to the following:
```d
int fun(ref int a, int b)
in {
   assert(a > 0);
   assert(b >= 0, "b cannot be negative!");
}
out(r) {
   assert(r > 0, "return must be positive");
   assert(a != 0);
}
do {
   // function body
}

interface I {
   int fun(int a)
   in {
      assert(a > 0);
   } 
}

class C {
   int data;
   invariant {
      assert(data != 0, "data cannot be 0");
   }
}
```
This DIP does not prohibit the use of validation constructs other than `assert` inside a contract, but is based on the assumption that `assert` is the norm. Suggesting a means to replace `assert` in the lowered code with an alternative is beyond the scope of this DIP, but remains an option for language implementers.

## Analysis

The benefits of the new syntax are obvious. By removing the syntactic noise from the grammar, an increase in the usage of contracts can be expected.

A drawback of this conciseness is that a small usability gap will open up between those who find the built-in `assert` functionality adequate to their needs, and those who require some other system of checking. The existing `in` and `out` contract syntax is still available for use by the latter. But the language now more strongly promotes the implicit `assert` functionality. The incentive is increased, therefore, to improve `assert` so that it serves as many people as possible.

### Analysis of the new `out` syntax

The most desirable syntax for the new `out` expression, i.e. a single parenthesized set of `assert` arguments,  is ambiguous with the existing `out` contract grammar. The presence of nothing but a single identifier inside the parentheses could be interpreted as either syntax. None of the proposed solutions is perfect.

1. The most straightforward solution is for the new grammar to require the condition to contain additional tokens when checking a non-return identifier. So instead of `out(i)`, for example, one must write `out(i != 0)`, and ambiguity is avoided. Any programmer who forgets to write those extra tokens will be met with a parsing error eventually, if not immediately.
```d
int fun(ref int i)
  out(i != 0) // okay
  out(i) // whoops! ambiguous with existing contract syntax
{
   // out contract, not function body
}
... // parsing error inevitable
```
The compiler can possibly use extra state logic to detect this particular error and assist the programmer with a helpful error message, "`out` contract expressions require more than just a single identifier", when the parsing finally fails.

But it is probably preferable to avoid this ambiguity altogether. Any proposed solution to the ambiguous `out` syntax must accomodate declaring the return identifier regardless. And all such methods of declaring return identifiers have the added advantage of parsing unambiguously with the existing syntax. Thus, simply by omitting the return identifier, they can be used unambiguously to check other variables as well, and are therefore recommended over option 1. Two possibilities of this type follow.

2. Use two sets of parentheses. This resembles the way the existing `out` contract syntax declares the return identifier. It also resembles the syntax of template function declarations.
```d
int fun(ref int i)
  out(r)(r > 0)
  out()(i > 0) // unambiguous parsing of a non-return identifier
{
  ...
}
```
The two parentheses style is slightly more verbose than the semicolon style which follows.

3. The semicolon style has already been introduced:
```d
int fun(ref int i)
  out(r; r > 0)
  out(; i > 0) // unambiguous parsing of a non-return identifier
{
  ...
}
```
It is the recommended option for this DIP because, while competitive with option 2,
* it is more concise, requiring only `;` to separate the return identifier
* the semantics of the new `out` expression resemble the semantics of `for` and `foreach`
* it is attractive for all new constructs to require the use of exactly one set of parentheses 

Note that existing `foreach` statements do not allow omitting the initial identifier — whereas the proposed `out` syntax does. If total consistency is desired, either both can be forced to declare an identifier that isn't used — or `foreach` can be changed, to allow omitting the identifier from it as well.

## Grammar
The grammar for `assert` parameters is redefined as ContractParameters, which is used in all the new constructs. Existing `in`, and `out` statements remain as they are. `invariant` statements are amended with the new variation. The `foreach` version of the new syntax is described here. New grammar follows:
```
ContractParameters:
    AssignExpression ,opt
    AssignExpression , AssignExpression ,opt

AssertExpression(redefined):
    assert ( ContractParameters )

InExpression:
    in ( ContractParameters )

OutExpression:
    out ( ; ContractParameters )
    out ( Identifier ; ContractParameters )

Invariant(redefined):
    invariant ( ) BlockStatement
    invariant BlockStatement
    invariant ( ContractParameters ) ;

FuncDeclaratorSuffix(redefined):
    Parameters MemberFunctionAttributes,opt ContractExpressions,opt
    TemplateParameters Parameters MemberFunctionAttributes,opt Constraint,opt ContractExpressions,opt

ContractExpressions:
    ContractExpression
    ContractExpression ContractExpressions

ContractExpression:
    InExpression
    OutExpression
```
## Code Breakage

None of the proposed syntax is currently compilable. Therefore no code is expected to break.

## Acknowledgements

This DIP was based upon an idea initially proposed by H.S. Teoh. It incoporates ideas from Moritz Maxeiner, Solomon E, Timon Gehr, and the DIP author, and was very helpfully edited by Mike Parker.

## Copyright & License

Copyright (c) 2017 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

[Preliminary Review Round 1](http://forum.dlang.org/post/gjtsfysvtyxcfcmuutez@forum.dlang.org)

[Preliminary Review Round 2](http://forum.dlang.org/thread/luhdbjnsmfomtgpydser@forum.dlang.org)

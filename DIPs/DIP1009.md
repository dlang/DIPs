# Improve Contract Syntax

| Field             | Value                                                           |
|-------------------|-----------------------------------------------------------------|
| DIP:              | 1009                                                            |
| Review Count:     | 1 [Most Recent]                                                 |
| Author:           | Zach Tollen - reachzach@gmail.com                               |
| Draft 2 Ideas by: | H.S. Teoh, Moritz Maxeiner, Solomon E, Zach Tollen, Timon Gehr  |
| Implementation:   | [Timon Gehr]                                                    |
| Status:           | Preliminary Review Round 2                                      |

[Most Recent]: https://github.com/dlang/DIPs/blob/d2dc77802c74378cf4545069eced21f85fbf893f/DIPs/DIP1009.md

[Timon Gehr]: https://github.com/dlang/dmd/compare/master...tgehr:contract-syntax

## Abstract

D has already made a significant commitment to the theory of [Design by Contract](http://wiki.c2.com/?DesignByContract), by means of its existing `in`, `out`, and `invariant` constructs. But limitations remain to their practical usability. This DIP intends to redress the syntax aspect of those limitations. A new syntax for `in`, `out`, and `invariant` contracts is proposed, making them much easier to read and write.

### Links

[DIP1003 discussion thread](http://forum.dlang.org/post/cklhgfbnpajbeefmwjrf@forum.dlang.org), which was the impetus for this DIP.

## Rationale

D's existing `in` and `out` contract syntaxes are hard to use for three reasons. The first is that one must create a block statement for any contract, even a simple one-liner. The largest share of contracts will be one line long. In order to keep this manageable for codebases that use them, a strict formatting style must be used. For example, the [SDC](https://github.com/SDC-Developers/SDC/) codebase, writes contracts [like this:](https://github.com/SDC-Developers/SDC/blob/master/src/d/parser/expression.d#L1059)
```d
ulong strToDecInt(string s) in {
    assert(s.length > 0, "s must not be empty");
} body {
    ulong ret = 0;
    ...
}  
```
The same contract in any other formatting style, including the standard style of DMD and Phobos, would cause them to take up so much space that they would not be worthwhile:
```d
ulong strToDecInt(string s)
in
{
    assert(s.length > 0, "s must not be empty");
}
body
{
    ulong ret = 0;
}
```
The second reason contracts are hard to use is that they require the use of the keyword `do` (or `body`, see [DIP1003](https://github.com/dlang/DIPs/blob/master/DIPs/DIP1003.md)) before the function body. Any way of writing them that could eliminate this need would be an improvement.

Finally, `in` and `out` contracts in D require explicitly writing `assert` statements, or some equivalent functionality, in their code block in order to perform checks. But because of the nature of contracts, `assert` can be assumed in the vast majority of cases, and it is tedious to have to write it specifically. The means to use forms of checking other than `assert` must be retained for those who need them. But for the common case, the mere presence of `in` or `out` is sufficient.

All of these limitations can be removed by adding new syntax. History shows that a small change in syntax can dramatically change programmers' willingness to use language features. An increase in usage of D's contracts can be expected.

Furthermore, because the existing `invariant` grammar is so similar to `in` and `out` contract grammar, any improvements to `in` and `out` contracts can be transferred easily to `invariant`s as well. Consistency across language constructs is desirable.

## Description

Allow `in`, `out`, and `invariant` contracts to be written as parenthesized expressions. Allow multiple expressions of this kind. Create a new grammatical construct called ContractParameters, which copies and replaces the parameters to `assert` expressions. Use the new construct for `in`, `out`, `assert`, and `invariant` expressions alike. `in` and `out` expressions must come at the end of the function declarator suffix, and before the regular contracts, if any. `invariant` expressions require a semicolon terminator. The keyword `do` or `body` is not required after the new `in` and `out` expressions. The choice of syntax for `out` expressions requires a separate analysis (see the Analysis section below). For the sake of the following example, the `out` expression syntax requires a semicolon separating an optional return identifier from its subsequent ContractParameters:

```d
int fun(ref int a, int b)
   in(a > 0)
   in(b >= 0, "b cannot be negative!")
   out(r; r > 0, "return must be positive")
   out(; a !=0)
{
   // function body
}

class C {
   int data;
   invariant(data != 0, "data cannot be 0");
}
```
In existing D, the above contracts would have to be written as (and would in most cases lower to):
```d
int fun(ref int a, int b)
in {
   assert(a > 0);
   assert(b >= 0, "b cannot be negative!");
}
out(r) {
   assert(r > 0, "return must be positive");
   assert(a != 0);
}
do {
   // function body
}

class C {
   int data;
   invariant {
      assert(data != 0, "data cannot be 0");
   }
}
```

Note that while the new `in`, `out`, and `invariant` expressions use the same grammar as `assert`, this DIP does not declare that the ready-made `assert` checking logic must be used to implement them. It is assumed that `assert` will be the default, but different runtime hooks for the new expressions are still permitted.

## Analysis

The benefits of the new syntax are rather obvious. By removing the syntactic noise from the grammar, an increase in the usage of contracts can be expected.

The drawback of this conciseness is that a usability gap may open up between those who find the built-in `assert` functionality adequate to their needs, and those who, for whatever reason, are forced to use some other system of checking. The existing `in` and `out` contract syntax is still available for use by the latter. But the language now more strongly promotes using existing `assert` functionality where possible. The incentive is increased, therefore, to make `assert` serve as many people as possible.

### Analysis of the new `out` syntax

The most desirable syntax for the new `out` expression, i.e. a single parenthesized set of `assert` arguments,  is ambiguous with the existing `out` contract grammar. The presence of nothing but a single identifier inside the parentheses could be interpreted as either syntax. None of the proposed solutions is perfect.

1. The simplest solution is for the new grammar to require the condition to contain additional tokens when checking a non-return identifier. So instead of `out(i)`, for example, one must write `out(i != 0)`, and ambiguity is avoided. Any programmer who forgets to write those extra tokens will be met with a parsing error eventually, if not immediately.
```d
int fun(ref int i)
  out(i != 0) // okay
  out(i) // whoops! ambiguous with existing contract syntax
{
   // out contract, not function body
}
... // parsing error inevitable
```
The compiler can possibly use extra state logic to detect this particular error and assist the programmer with a helpful error message, "`out` contract expressions require more than just a single identifier", when the parsing finally fails. Very few exit contracts will, however, check outgoing variables with nothing but a single identifier. Therefore this will be the rare case.

While this works in the case of non-return identifiers,  proposed solutions must still accomodate declaring the return identifier. All such methods of declaring return identifiers have the advantage of parsing unambiguously with the existing syntax. Thus, simply by omitting the return identifier, they can be used unambiguously to check other variables, which is why the example in the description above used this method. The examples which follow show both the unambiguous way for each method, and the ambiguous way described in 1 above, for comparison.

2. The semicolon style has already been introduced, and is tentatively recommended because:
* it is the most concise of the alternatives, requiring only `;` to separate the return identifier
* the semantics of the new `out` expression at least slightly resemble the semantics of `foreach` loops
```d
int fun(ref int i)
  out(r; r > 0)
  out(; i > 0) // unambiguous parsing
  out(i > 0) // ambiguous alternative, but possibly manageable (see 1 above)
{
  ...
}
```
Note that existing `foreach` statements do not allow omitting the initial identifier, whereas the proposed `out` syntax does allow this. If the ambiguous parsing alternative is rejected, and total consistency is desired, one can either force both to declare an identifier that isn't used, or change `foreach` to allow omitting the identifier.

3. Another possibility is to use two sets of parentheses, which would resemble the existing `out` contract syntax, but also template function declarations. In the absence of a return identifier, one could either require empty parentheses or allow omitting them, in accordance with the restrictions of solution 1 above:
```d
int fun(ref int i)
  out(r)(r > 0)
  out()(i > 0) // unambiguous parsing 
  out(i > 0) // ambiguous alternative
{
  ...
}
```
4. Finally, use the lambda syntax in place of the ordinary `assert` condition, e.g.:
```d
int fun(ref int i)
  out(r => r > 0, "blah")
  out(() => i > 0, "blah") // unambiguous
  out(i > 0) // ambiguous alternative
{
  ...
}
```
The advantage of this is that lambda expression semantics most closely resemble `out` contract semantics. The disadvantage of this variation is that the unambiguous parsing version (`out(() => i > 0, "blah")`), if deemed necessary (see 1 above), is very awkward and verbose.

One way to implement this variation is simply to treat the `=>` token as equivalent to `;` in the foreach version. That way the new construction will not require semantics of its own. By contrast, the implementation of such semantics, if deemed necessary, would require an `out` function signature [somewhat like:](https://github.com/dlang/DIPs/pull/76#issuecomment-314039875) 
```d
auto out(auto ref Return value, bool delegate(auto ref Return) cond, string msg) {
    assert(cond(value), msg);
}
```

## Grammar
The grammar for `assert` parameters is redefined as ContractParameters, which is used in all the new constructs. Existing `in`, and `out` statements remain as they are. `invariant` statements are amended with the new variation. The `foreach` version of the new syntax is described here. New grammar follows:
```
ContractParameters:
    AssignExpression ,opt
    AssignExpression , AssignExpression ,opt

AssertExpression(redefined):
    assert ( ContractParameters )

InExpression:
    in ( ContractParameters )

OutExpression:
    out ( ; ContractParameters )
    out ( Identifier ; ContractParameters )

Invariant(redefined):
    invariant ( ) BlockStatement
    invariant BlockStatement
    invariant ( ContractParameters ) ;

FuncDeclaratorSuffix(redefined):
    Parameters MemberFunctionAttributes,opt ContractExpressions,opt
    TemplateParameters Parameters MemberFunctionAttributes,opt Constraint,opt ContractExpressions,opt

ContractExpressions:
    ContractExpression
    ContractExpression ContractExpressions

ContractExpression:
    InExpression
    OutExpression
```
## Code Breakage

None of the new syntax is currently compilable, so no code is expected to break.

## Copyright & License

Copyright (c) 2017 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

[Preliminary Review Round 1](http://forum.dlang.org/post/gjtsfysvtyxcfcmuutez@forum.dlang.org)

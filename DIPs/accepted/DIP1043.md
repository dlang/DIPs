# Shortened Method Syntax

| Field           | Value                                                          |
|-----------------|----------------------------------------------------------------|
| DIP:            | 1043                                                           |
| Review Count:   | 2                                                              |
| Author:         | Max Haughton, Adam D. Ruppe                                    |
| Implementation: | https://github.com/dlang/dmd/pull/11833                        |
| Status:         | Approved                                                       |

## Abstract
This DIP proposes a shortened syntax for function definitions.
The new syntax makes use of `=>` to separate the function header and function body in place of opening and closing braces:

```D
int add(int x, int y) pure => x + y;
```

The feature is already implemented in the D programming language as a preview.

## Contents
* [Rationale](#rationale)
* [Prior Work](#prior-work)
* [Description](#description)
* [Reference](#reference)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale
### Consistency
A shortened syntax for function literals is already supported in the D programming language. For example:

```D
const succ = (int x) => x + 1;
```

is equivalent to

```D
const succ = function(int x) { return x + 1; };
```

Via a trivial change to the language, a similar syntax can be implemented for function definitions,
making function literals and function definitions more uniform.

### Brevity
Consider a simple `InputRange` which produces a range of `T`s that excludes one end (`[from, to)`).
An implementation in D without the proposed syntax is 14 lines:

```d
struct LongerExclusiveRange(T)
{
	T from, to;
   invariant(from <= to);
   bool empty() {
      return from == to;
   }
   void popFront() {
      ++from;
   }
   T front() {
      return from;
   }
}
```

An implementation utilizing the proposed feature is only 8 lines:

```d
struct ExclusiveRange(T)
{
	T from, to;
   invariant(from <= to);
   bool empty() => from == to;
   auto popFront() => ++from;
   T front() => from;
}
```
Note that some style guides may permit both braces of a function on the same line. In this case, the use of this feature may not result in a saving of lines, but there will be fewer keystrokes by virtue of there being no braces.

More fundamentally than mere line or even character counting, however, use of this syntax reduces a function to the essence of its action. In short, the tokens remaining are the ones [that do work](https://en.wikipedia.org/wiki/Work_(physics)). A function that does not require the use of braces (a block statement) or even a statement at all may simply avoid their use entirely. This is a small step towards reducing obstreperous visual noise that can plague virtually any program.

For example, take this arbitrary yet representative composition of ranges:

```d
auto doesWork()
    => iota(1, 100)
        .map!(x => x + 1)
        .filter!(x => x > 4)
        .each!writeln;
```

With the shortened methods syntax, this function terminates syntactically in the same place it does semantically.
The call to `each` is where the function's work ends. With the proposed syntax. this is also where it ends lexically (barring the semi-colon).

## Prior Work
This DIP has the potential to result in a post-facto blessing of a preview feature already supported by the compiler.
[The existing implementation](https://github.com/dlang/dmd/pull/11833) was written by Adam D. Ruppe.

The proposed feature is present in the C# programming language, where instances of its use are referred to as
[expression-bodied members](https://docs.microsoft.com/en-us/dotnet/csharp/programming-guide/statements-expressions-operators/expression-bodied-members).

The C# documentation demonstrates an idiomatic example of the proposed feature, reproduced here:

```c#
public class Location
{
   private string locationName;

   public Location(string name)
   {
      locationName = name;
   }

   public string Name => locationName;
}
```

Note that the `Name` function is a simple one-liner that returns the `locationName` member. Such one-liners are common in C#,
where programmers are encouraged to provide access to member variables via member functions.

## Description

### Semantics
The proposed semantics are a simple example of what is referred to in the theory of programming language implementation as lowering:

```d
FunctionDeclarator => AssignExpression;
```

shall be rewritten to

```d
FunctionDeclarator
{
	return AssignExpression;
}
```

Given that constructors and destructors cannot have return values, the implementation should reject any attempt to implement a constructor or destructor using the shortened method syntax and provide a meaningful error message.

#### Contract programming
It has been noted (see below) that the preview implementation allows the use of
function contracts with this syntax. Contract programming is not available in the use of [shortened function literals](https://dlang.org/spec/expression.html#function_literals) (i.e. `x => x * 2`), so this DIP does not allow them. Since this DIP aims to make expressions and declarations consistent, use of contact programming is not proposed on the shortened form.

### Grammar
The proposed feature requires the following grammar changes:
```diff
FunctionBody:
     SpecifiedFunctionBody
     MissingFunctionBody
+    ShortenedFunctionBody
...

+ShortenedFunctionBody:
+    => AssignExpression ;
```


## Reference
* Initially https://github.com/dlang/dlang.org/pull/3059 and finally https://github.com/dlang/dlang.org/pull/3198 are two PRs that document theacceptance of contracts on the shortened function syntax.

### Trivia
* [The syntax was mentioned on the D Bugzilla in 2011](https://issues.dlang.org/show_bug.cgi?id=7176). The dialectics in this thread can be considered a pre-review.
* [Shortened Methods are documented in the specification](https://github.com/dlang/dlang.org/pull/2956)
   Note the validity of contract programming, which is not exactly compliant with this DIP.
* [A dmd changelog entry was added](https://github.com/dlang/dmd/pull/12241) on the 28th of February 2021

## Copyright & License
Copyright (c) 2022 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews

### Community Review Round 1
[Reviewed Version](https://github.com/dlang/DIPs/blob/2e6d428f42b879c0220ae6adb675164e3ce3803c/DIPs/DIP1042.md)

[Discussion](https://forum.dlang.org/post/jrigjbciylxzwubuopez@forum.dlang.org)

[Feedback](https://forum.dlang.org/post/picueoqamrouueyntjmk@forum.dlang.org)

The following actionable feedback was provided in the feedback thread:

* The example provided in the Rationale is misleading in its implication that the features saves lines of code, as the original functions could be written as one lines; as such, the example should be dropped. The DIP author responded that the feature still saves on typing, and also that some coding standards may prohibit one-liners with braces.
* The DIP's claim that "function contracts are not possible in function literals" is inaccurate; they simply aren't allowed with the arrow syntax.

### Final Review
[Reviewed Version](https://github.com/dlang/DIPs/blob/2c2f6c33f5761236266a96bd268c62a06323a5e8/DIPs/DIP1043.md)

[Discussion](https://forum.dlang.org/thread/drvmkookrgasjeeuuutn@forum.dlang.org)

[Feedback](https://forum.dlang.org/post/xxfdyxrnskdxzvdigwld@forum.dlang.org)

The following items were provided in the feedback thread:

* Line-counting isn't a contrived argument and should be removed.
* References to more languages should be added, e.g., Dart.
* The DIP should also include arguments against the feature.
* The DIP should explicitly mention that it allows function templates.

### Formal Assessment
The language maintainers accepted this DIP on the grounds that the proposed feature had already been implemented as a preview feature for some time without problem or complaint. One of the maintainers suggested an enhancement to the proposal in the form of shortening the syntax even further, such that `T front() => from` would become `T front => from`. The DIP author decided against this, leaving the possibility open for future enhancement.
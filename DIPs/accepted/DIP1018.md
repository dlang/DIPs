# The Copy Constructor

| Field           | Value                                                                           |
|-----------------|---------------------------------------------------------------------------------|
| DIP:            | 1018                                                                            |
| Review Count:   | 2                                                                               |
| Authors:        | Razvan Nitu (razvan.nitu1305@gmail.com), Andrei Alexandrescu (andrei@erdani.com)|
| Implementation: | https://github.com/dlang/dmd/pull/8688                                          |
| Status:         | Accepted                                                                        |

## Abstract

This document proposes the copy constructor, an alternative to the postblit function. The copy constructor obviates the need for postblit and fixes its flaws and inherent limitations. Evolution and backward compatibility are also discussed.

## Contents
* [Rationale and Motivation](#rationale-and-motivation)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Reviews](#reviews)

## Rationale and Motivation

This section highlights the existing problems with the postblit and demonstrates
why the implementation of a copy constructor is more desirable than an attempt
to fix postblit's issues on a case basis.

### Overview of `this(this)`

The postblit is a function that cannot be meaningfully overloaded or qualified. However, the compiler does not reject certain applications of qualifiers, as illustrated below:

```D
struct A { this(this) const {} }
struct B { this(this) immutable {} }
struct C { this(this) shared {} }
```

The semantics of the postblit in the presence of qualifiers is
not defined, and experimentation reveals that the behavior is a patchwork of happenstance semantics:

* `const` postblits are not able to modify any fields in the destination
* `immutable` postblits never get called (resulting in compilation errors)
* `shared` postblits cannot guarantee atomicity while blitting the fields

Defining and implementing some meaningful semantics will break code that has been tested and deemed correct under the current semantics.

#### Considering `const`/`immutable` postblits

A solution for `const` and `immutable` postblits would be to type check them as normal
constructors, where the first assignment of a member is considered an initialization
and subsequent assignments count as modifications. This is problematic because
after the blitting phase, the destination object is no longer in its initial state
and subsequent assignments to its fields will be regarded as modifications, making
it impossible to construct nontrivial `immutable`/`const` objects in the postblit. In addition,
it is possible for multiple postblits to modify the same field. Consider:

```D
struct A
{
    immutable int a;
    this(this)
    {
        this.a += 2;  // modifying immutable, an error or OK?
    }
}

struct B
{
    A a;
    this(this)
    {
        this.a.a += 2;  // modifying immutable, an error or OK?
    }
}

void main()
{
    B b = B(A(7));
    B c = b;
}
```

When `B c = b;` is encountered, the following actions are taken:

1. `b`'s fields are blitted to `c`
2. A's postblit is called
3. B's postblit is called

After step 1, the object `c` has the exact contents as `b`, but it is neither
initialized (the postblits still need to run) nor uninitialized (the field
`B.a` does not have its initial value). From a type checking perspective
this is a problem because the assignment inside `A`'s postblit is breaking immutability.
This makes it impossible to postblit objects that have `immutable`/`const` fields.
To alleviate this problem one could consider that after the blitting phase the object is
in a raw state, therefore uninitialized; this way, the first assignment of `B.a.a`
is considered an initialization. However, after this step the field
`B.a.a` is considered initialized, therefore how is the assignment inside `B`'s postblit
supposed to be type checked? Is it a violation of immutability, or should
it be legal? Indeed, it is breaking immutability because it is changing an immutable
value. However as this is part of initialization (recall that `c` is initialized only
after all the postblits are ran) it should be legal, thus weakening the immutability
concept and creating a different strategy from that implemented by normal constructors.

#### Shared postblits

Shared postblits cannot guarantee atomicity while blitting the fields because
that part is done automatically and it does not involve any
synchronization techniques. The following example demonstrates the problem:

```D
shared struct A
{
    long a, b, c;
    this(this) { ... }
}

A a = A(1, 2, 3);

void fun()
{
    A b = a;
    /* do other work */
}
```

Let's consider the above code is run in a multithreaded environment
When `A b = a;` is executed, the following actions are carried:

* `a`'s fields are copied to `b` bitwise ("blitted")
* `this(this)` is called

In the blitting phase, no synchronization mechanism is employed, which means
that while copying is in progress, another thread may modify `a`'s data, resulting
in the corruption of `b`. There four possible approaches to fixing this issue:

1. Make `shared` objects larger than two words noncopyable. This solution cannot be
taken into account because it imposes a major arbitrary limitation: almost all
`struct`s would become noncopyable.

2. Allow incorrect copying and expect that the user will
do the necessary synchronization from the outside. Example:

```D
shared struct A
{
    Mutex m;
    long a, b, c;
    this(this) { ... }
}

A a = A(1, 2, 3);

void fun()
{
    A b;
    a.m.acquire();
    b = a;
    a.m.release();
    /* do other work */
}
```

Although this solution solves the synchronization problem, it does so in a manner
that requires unencapsulated attention at each copy site. A distinct performance problem is
created by releasing the mutex after the postblit is
run, which increases the amount of work in the critical section. The release should at best occur immediately following
the blitting phase (the first line of the postblit) because the copy is
thread-local, but this results in non-scoped locking: the mutex is released
in a different scope than that in which it was acquired. Also, the mutex is
automatically (and wrongfully) copied.

3. Introduce a preblit function that would be called before blitting the fields.
The purpose of the preblit is to offer the possibility of preparing data
before the blitting phase; acquiring the mutex on the `struct` to
copy is one of the operations that the preblit would be responsible for. Later
on, the mutex would be released in the postblit. This approach has the benefit of
minimizing the critical section in a manner that offers encapsulation, but
suffers the disadvantage of adding even more complexity by introducing a new
concept which requires type checking disparate sections of code (need to type check
across preblit, blit, and postblit).

4. Use an implicit global mutex to synchronize the blitting of fields. This approach
has the advantage that the compiler will do all the work and synchronize all blitting
phases (even if the threads don't actually touch each other's data) at a cost to
performance. Python implements a global interpreter lock which is criticized for its
nonscalable high contention; there are ongoing discussions of removing it from the Python
implementation [5].

### Introducing the copy constructor

As discussed above, the postblit is difficult to type check without unreasonable restrictions and cannot be synchronized without undue costs.

This DIP proposes a copy constructor with the following benefits:

* the feature is used to good effect in the C++ language [3];
* the copy constructor can be type checked as a normal constructor (since no blitting is done, the fields are
initialized the same as in a normal constructor); this means that `const`/`immutable`/`shared`
copy constructors will be type checked exactly as their analogous regular constructors;
* offers encapsulation.

The downside of this solution is that the user must copy all fields by hand, and every
time a field is added to a `struct`, the copy constructor must be modified. However, this issue
can be easily worked around by using D's introspection mechanisms. For example, this simple code
may be used as a language idiom or library function:

```D
foreach (i, ref field; src.tupleof)
    this.tupleof[i] = field;
```

As shown above, the benefit of the postblit can be easily substituted with a
few lines of code. As discussed in detail below, copy constructors offer solutions to all of postblit's limitations with minimal increase in language complexity.

## Description

This section discusses all the technical aspects regarding the semantics
of the copy constructor.

### Syntax

Inside a `struct` definition, a declaration is a copy constructor declaration if it is a constructor declaration that takes the first parameter as a nondefaulted reference to the same type as the `struct` being defined. Additional parameters may follow if and only if all have default values. Declaring a copy constructor in this manner has the advantage that no parser
modifications are required, thus leaving the language grammar unchanged. Example:

```d
import std.stdio;

struct A
{
    this(ref A rhs) { writeln("x"); }                     // copy constructor
    this(ref A rhs, int b = 7) immutable { writeln(b); }  // copy constructor with default parameter
}

void main()
{
    A a;
    A b = a;            // calls copy constructor implicitly - prints "x"
    A c = A(b);         // calls constructor explicitly
    immutable A d = a;  // calls copy constructor implicittly - prints 7
}
```

The copy constructor may also be called explicitly (as shown above in the line introducing `c`) because it is also a constructor within the preexisting language semantics.

The argument to the copy constructor is passed by reference in order to avoid infinite recursion (passing by
value would require a copy of the `struct`, which would be made by calling the copy constructor, thus leading
to an infinite chain of calls). Note that if the source is an rvalue, no call to the copy constructor is necessary
because the value will be bitwise moved (if necessary) into the destination. Example:

```d
struct A { this(ref A rhs) {} }

A create()
{
    static A prototype;
    return prototype;
}

void main()
{
    A value = create();
}
```

Exactly one call to the copy constructor is made, with `prototype` in the `rhs` position. Then the rvalue obtained is seated into `value`.

Type qualifiers may be applied to the parameter of the copy constructor and also to the function itself,
in order to allow defining copies across objects of different mutability levels. The type
qualifiers are optional.

### Semantics

This section discusses all the aspects of the semantic analysis and interaction between the copy
constructor and other language features.

#### Copy constructor and postblit cohabitation

In order to ensure a smooth transition from postblit to copy constructor, this DIP proposes the
following strategy: if a `struct` defines a postblit (either user-defined or generated), all
copy constructor definitions will be ignored for that particular `struct` and the postblit
will be preferred. Existing code bases that do not use the postblit may start using the
copy constructor, whereas codebases that currently rely on the postblit may
start writing new code using the copy constructor and remove the postblit
from their code. This DIP recommends deprecation of postblit but does not prescribe a specific deprecation schedule.

A semantically close transition path from postblit to copy constructor may be achieved as follows:

```d
// Existing code
struct A
{
    this(this) { ... }
    ...
}
// Replacement code
struct A
{
    this(ref inout A rhs) inout { ... }
}
```

#### Copy constructor usage

A call to the copy constructor is implicitly inserted by the compiler whenever a `struct` variable is initialized
as a copy of another variable of the same unqualified type:

1. When a variable is explicitly initialized:

```d
struct A
{
    this(ref A rhs) {}
}

void main()
{
    A a;
    A b = a; // copy constructor gets called
    b = a;   // assignment, not initialization
}
```

2. When a parameter is passed by value to a function:

```d
struct A
{
    this(ref A another) {}
}

void fun(A a) {}

void main()
{
    A a;
    fun(a);    // copy constructor gets called
}
```

3. When a parameter is returned by value from a function and NRVO cannot be performed:

```d
struct A
{
    this(ref A another)
    {
        writeln("cpCtor");
    }
}

A fun()
{
    A a;
    return a;
}

A a;
A gun()
{
    return a;
}

void main()
{
    A a = fun();      // NRVO - no copy constructor call
    A b = gun();      // NRVO cannot be performed, copy constructor is called
}
```

The parameter of the copy constructor is passed by reference, so initializations will be
lowered to copy constructor calls only if the source is an lvalue. Although this can be
worked around by declaring temporary lvalues which can be forwarded to the copy constructor,
binding rvalues to lvalues is beyond the scope of this DIP.

Note that when a function returns a `struct` instance that defines a copy constructor and NRVO cannot be
performed, the copy constructor is called at the callee site before returning. If NRVO
may be performed, then the copy is elided:

```d
struct A
{
    this(ref A rhs) {}
}

A a;
A fun()
{
    return a;      // lowered to return tmp.copyCtor(a)
    // return A(); // rvalue, no copyCtor called
}

void main()
{
    A b = fun();    // b is constructed in place with the return value of fun
}
```

#### Type checking

The copy constructor type-check is identical to that of the constructor [[6](https://dlang.org/spec/struct.html#struct-constructor)]
[[7](https://dlang.org/spec/struct.html#field-init)].

Copy constructor overloads can be explicitly disabled:

```d
struct A
{
    @disable this(ref A rhs);
    this(ref immutable A rhs) {}
}

void main()
{
    A b;
    A a = b;     // error: disabled copy construction

    immutable A ia;
    A c = ia;  // ok

}
```

In order to disable copy construction, all copy constructor overloads must be disabled.
In the above example, only copies from mutable to mutable are disabled; the overload for
immutable to mutable copies is still callable.

#### Overloading

The copy constructor can be overloaded with different qualifiers applied to the parameter
(copying from a qualified source) or to the copy constructor itself (copying to a qualified
destination):

```d
struct A
{
    this(ref A another) {}                        // 1 - mutable source, mutable destination
    this(ref immutable A another) {}              // 2 - immutable source, mutable destination
    this(ref A another) immutable {}              // 3 - mutable source, immutable destination
    this(ref immutable A another) immutable {}    // 4 - immutable source, immutable destination
}

void main()
{
    A a;
    immutable A ia;

    A a2 = a;      // calls 1
    A a3 = ia;     // calls 2
    immutable A a4 = a;     // calls 3
    immutable A a5 = ia;    // calls 4
}
```
The proposed model enables the user to define the copy from an object of any qualified type
to an object of any qualified type: any combination of two among mutable, `const`, `immutable`, `shared`,
`const shared`.

The `inout` qualifier may be applied to the copy constructor parameter in order to specify that mutable, `const`, or `immutable` types are
treated the same:

```d
struct A
{
    this(ref inout A rhs) immutable {}
}

void main()
{
    A r1;
    const(A) r2;
    immutable(A) r3;

    // All call the same copy constructor because `inout` acts like a wildcard
    immutable(A) a = r1;
    immutable(A) b = r2;
    immutable(A) c = r3;
}
```

In the case of partial matching, existing overloading and implicit conversion
rules apply to the argument.

#### Copy constructor call vs. blitting

When a copy constructor is not defined for a `struct`, initializations are handled
by copying the contents from the memory location of the right-hand side expression
to the memory location of the left-hand side expression (i.e. "blitting"). Example:

```d
struct A
{
    int[] a;
}

void main()
{
    A a = A([7]);
    A b = a;                 // mempcy(&b, &a)
    immutable A c = A([12]);
    immutable A d = c;       // memcpy(&d, &c)
}
```

When a copy constructor is defined for a `struct`, all implicit blitting is disabled for
that `struct`. Example:

```d
struct A
{
    int[] a;
    this(ref A rhs) {}
}

void fun(immutable A) {}

void main()
{
    immutable A a;
    fun(a);          // error: copy constructor cannot be called with types (immutable) immutable
}
```

#### Interaction with `alias this`

A `struct` that defines both an `alias this` and a copy constructor may engender ambiguous situations. Ambiguity occurs when the type returned by `alias this` is a qualified version of the type being defined. Example:

```d
struct A
{
    int *a;
    immutable(A) fun()
    {
        return immutable A();
    }

    alias fun this;

    this(ref A another) immutable {}
}

struct B
{
    int *a;
    immutable(B) fun()
    {
        return immutable B();
    }

    alias fun this;

    this(ref B another) {}
}

void main()
{
    A a;
    immutable A ia = a;    // copy constructor
    B b;
    immutable B ib = b;    // error: copy constructor may not be called for argument types `() immutable`
}
```

When both the copy constructor and `alias this` are suitable to resolve the
assignment, the copy constructor takes precedence over `alias this` as it is
considered more specialized (the sole purpose of the copy constructor is to
create copies). If no copy constructor in the overload set is matched, an error
is issued, even if using `alias this` could theoretically lead to a matching constructor. In other words, `alias this` is intentionally ignored for all purposes of copy construction.

#### Generating copy constructors

A copy constructor is generated implicitly by the compiler for a `struct S` if all of the following conditions are met:

1. `S` does not explicitly declare any copy constructors;
2. `S` defines at least one direct member that has a copy constructor, and that member is not overlapped (by means of `union`) with any other member.

If the restrictions above are met, the following copy constructor is generated:

```d
this(ref inout(S) src) inout
{
    foreach (i, ref inout field; src.tupleof)
        this.tupleof[i] = field;
}
```

### Plain Old Data (POD)

A `struct` that defines a copy constructor is not a POD.

### Interaction with unions

If an `union S` has fields that define a copy constructor, whenever an object of type
`S` is initialized by copy, an error will be issued. The same rule applies to overlapped
fields (anonymous unions).

## Breaking Changes and Deprecations

1. The parameter of the copy constructor is passed by a mutable reference to the
source object. This means that a call to the copy constructor may legally modify
the source object:

```d
struct A
{
    int[] a;
    this(ref A another)
    {
        another.a[2] = 3;
    }
}

void main()
{
    A a, b;
    a = b;    // b.a[2] is modified
}
```

This is surprising and potentially error-prone behavior because changing the source of a copy is not customary and may surprise the user of a type. (For that reason, C++ coding standards adopt the convention of taking the source by means of reference to `const`; copy constructors that use non-`const` right-hand side are allowed but discouraged.) In D, `const` and `immutable` are more restrictive than in C++, so forcing `const` on the copy constructor's right-hand side would make simple copying task unduly difficult. Consider:

```d
class Window
{
    ...
}
struct Widget
{
    private Window display;
    ...
    this(ref const Widget rhs)
    {
        display = rhs.display; // Error! Cannot initialize a Window from a const(Window)
    }
}
```

Such sharing of resources across objects is a common occurrence, which would be impeded by forcing `const` on the right-hand side of a copy. (An inferior workaround would be to selectively cast `const` away inside the copy constructor, which is obviously undesirable.) For that reason this DIP proposes allowing mutable copy sources.

2. This DIP changes the semantics of constructors that receive a parameter by reference of type
`typeof(this)`. The consequence is that existing constructors might be called implicitly in some
situations:

```d
struct C
{
    this(ref C another)    // normal constructor before DIP, copy constructor after
    {
        import std.stdio : writeln;
        writeln("Yo");
    }
}

void fun(C c) {}

void main()
{
    C c;
    fun(c);
}
```

This will print `"Yo"` subsequent to the implementation of this DIP, whereas under the current semantics it prints nothing. A case can be
made that a constructor with the above definition could not be correctly used as anything else than a copy
constructor, in which case this DIP actually fixes the code.

## Copyright & License

Copyright (c) 2018 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

### References

* [1] https://github.com/dlang/dmd/pull/8032

* [2] https://forum.dlang.org/post/p9p64v$ue6$1@digitalmars.com

* [3] http://en.cppreference.com/w/cpp/language/copy_constructor

* [4] https://dlang.org/spec/struct.html#struct-postblit

* [5] https://news.ycombinator.com/item?id=15008636

* [6] https://dlang.org/spec/struct.html#struct-constructor

* [7] https://dlang.org/spec/struct.html#field-init

## Reviews

### Community Review Round 1
[Reviewed Version](https://github.com/dlang/DIPs/blob/07da1f2cabc8b1bc3ad66034598a133e5ad13356/DIPs/DIP1018.md)

[Discussion](https://forum.dlang.org/post/eoqddfqbjtgfydajozsn@forum.dlang.org)

One reviewer suggested that `const` postblits can be achieved via a shadow copy whose destructor is run just before it is used to initialize the new instance. The DIP author responded that this would make blitting useless by doing unnecessary work.

One complaint was that copy constructors can be too verbose when a type has a large number of fields. The response to that was that in those instances, `.tupleof` can be used to copy the fields from one instance to another.

Another criticism was that mutable sources of a copy should not be allowed. This generated much discussion about the transitiveness of `const` and the implications of requiring `const` sources. The DIP author's position was that the transitiveness of `const` and `immutable` renders impossible the safe copying of objects containing indirections.

There was an objection raised to the allowance of additional default parameters in a copy constructor and that when additional parameters are required, the type should define property functions for post-copy fix up. The DIP author agreed and noted that the DIP originally took that position, but decided there was more benefit to allowing default parameters since C++ does so. In those use-cases where they are required, D programmers with a C++ background will find the solution familiar.

A seeming contradiction was uncovered in the explanation of the behavior when no copy constructor is defined. In "Copy constructor call vs. blitting" the DIP states copying will take place via a blit, while in "Generating copy constructors" it lists conditions under which a copy constructor will be generated. The DIP author agreed it could be stated better, but that the examples should be obvious that when no copy destructor is defined by a struct or any of its fields, blitting is employed, but if at least one field defines a copy constructor then a copy constructor is generated. The DIP author did not revise the DIP with this clarification.

One reviewer was curious if structs that defined copy constructors could still be manually moved and suggested the DIP should explain this scenario. The DIP author said these are orthogonal concepts, adding that D has no move operator and the copy constructor is used only when a copy is performed. He also suggested this DIP should not interfere with [DIP 1014](https://github.com/dlang/DIPs/blob/master/DIPs/accepted/DIP1014.md)'s `opMove`.

One commenter asked about a default copy destructor. The DIP author replied that there is no default copy constructor and reiterated that a copy constructor is only generated if a type does not have one but one of its fields does.

### Final Review
[Reviewed Version](https://github.com/dlang/DIPs/blob/4dffe2455919a40fff37837d4b0307a31f2839b3/DIPs/DIP1018.md)

[Discussion](https://forum.dlang.org/post/ccldcdawkytzurtrmtuf@forum.dlang.org)

The only feedback was a discussion centered on the decision to allow copy constructors to accept non-const arguments. Prior to the review, one of the DIP authors updated the DIP to add more motivation for the decision.

As a counter to complaints about allowing non-const arguments in copy constructors, one reviewer noted that the DIP does not require it. Copy constructors can be implemented with const arguments.

It was noted that the language is in need of a means to express mutable data in an immutable graph, to which one of the DIP authors responded that a separate DIP for `__mutable` is being drafted.

### Formal Assessment
The language maintainers considered the feature proposed here a top priority and worked with the DIP author to refine the implementation.The only change requested in the proposal was a comment that `A b = fun()` moves the result of `fun` into `b` be corrected, as `b` is constructed in place and no move is made.

When they were satisfied with the implementation, the DIP was given formal approval without a Final Review round in the interest of releasing the implementation as soon as possible. After feedback from community members, the approval was reverted and a Final Review round took place. 

There were no critical issues noted in the Final Review, so the DIP approval was reinstated.

# Tuple Unpacking Syntax

| Field           | Value                                                           |
|-----------------|-----------------------------------------------------------------|
| DIP:            | 1053                                                            |
| Review Count:   | 3                                                               |
| Authors:        | Jared Hanson, Nick Treleaven, Timon Gehr                        |
| Implementation: | https://github.com/tgehr/dmd/tree/unpacking                     |
| Status:         | Accepted                                                        |

## Abstract

This DIP proposes built-in language support for tuple unpacking.

## Contents

* [Rationale](#rationale)
* [Prior Work](#prior-work)
* [Description](#description)
* [Breaking Changes and Deprecations](#breaking-changes-and-deprecations)
* [Reference](#reference)
* [Copyright & License](#copyright--license)
* [History](#history)

## Rationale

The D programming language currently supports tuples with the library primitive [`std.typecons.Tuple`](https://dlang.org/phobos/std_typecons.html#.Tuple); however,
unpacking elements has to be done manually and is somewhat inconvenient:

  ```d
  import std.typecons : Tuple;

  Tuple!(int, string) foo();

  auto ab = foo(), a = ab[0], b = ab[1];

  // Specifying types (or differing storage classes) requires separate declarations
  auto cd = foo();
  int c = cd[0];
  string d = cd[1];
  ```

Additionally, unpacking nested tuples is particularly inconvenient, requiring multiple temporary variable declarations.

With the new syntax introduced by this DIP, the above code becomes much more concise and readable:

  ```d
  import std.typecons : Tuple;

  Tuple!(int, string) foo();

  auto (a, b) = foo();
  // or
  (int c, string d) = foo();
  ```

Full tuple syntax built into the language has been an oft-requested feature as far back as 2010 (see the [Links](#Links) section for related forum threads).
However, tuple literal syntax using parentheses, e.g., `(1, 2)` was originally blocked by the comma operator. Now that using the result of a comma expression
[has been made an error](https://dlang.org/changelog/2.079.0.html#comma-deprecation-error) as of March 2018, we are able to make progress with this first
step toward full tuple support in the D language.

(Note that unpacking syntax must be consistent with tuple literals and/or tuple type syntax, if either of those features are supported in future).

## Prior Work

Equivalent features in a selection of statically typed languages:

* C++ Structured Binding
    * `auto [x, y] = a;`
    * <https://en.cppreference.com/w/cpp/language/structured_binding>
* C# Tuple Deconstruction
    * `var (name, address, city, zip) = contact.GetAddressInfo();`
    * <https://learn.microsoft.com/en-us/dotnet/csharp/fundamentals/functional/deconstruct#tuples>
* Kotlin Destructuring Declarations
    * `val (name, age) = person`
    * <https://kotlinlang.org/docs/destructuring-declarations.html>
* Scala Tuple Pattern
    * `val (name, quantity) = ingredient`
    * <https://docs.scala-lang.org/tour/tuples.html#pattern-matching-on-tuples>
* Rust Tuple Pattern
    * `let (a, b) = pair;`
    * <https://doc.rust-lang.org/nightly/reference/patterns.html#grammar-TuplePattern>

## Description

This DIP includes a series of proposals that add tuple unpacking syntax to D. Note that this is not a full tuple syntax DIP&mdash;it is _solely_ limited to unpacking syntax.
A DIP for built-in tuples and the accompanying syntax may be proposed at a later date.

Goals of this DIP:

- allow unpacking a value sequence into new variable declarations conveniently, as in other modern languages with built-in tuple destructuring support;
- support unpacking a `std.typecons.Tuple`;
- minimize language changes while achieving the desired semantics.

### Unpacking declarations

We add the following syntactic sugar to unpack a value sequence (including via `alias this`).
An unpack declaration declares (at least) one variable for each element of a sequence
expression.

- Variables are declared in a tuple pattern.
- A pattern is an optional storage class followed by parentheses containing 1 or more components.
    - Each pattern component is separated by a comma.
    - For a single component, a comma must precede the closing parenthesis.
- A component is either a nested pattern, or it consists of:
    - a storage class (may be optional);
    - a type (may be optional);
    - an identifier.

Example:

```d
import std.typecons : tuple;

(int a, string b) = tuple(1, "2");
assert(a == 1);
assert(b == "2");
```

When there is a storage class for a pattern, each component of the pattern can
simply be an identifier. Each variable's type will be inferred from the corresponding
sequence element:

```d
auto (a, b) = tuple(1, "2");
static assert(is(typeof(a) == int));
static assert(is(typeof(b) == string));

auto (a, immutable b, c) = tuple(1, "2", 3.0);
static assert(is(typeof(a) == int));
static assert(is(typeof(b) == immutable string));
static assert(is(typeof(c) == double));
```

The number of values has to match exactly&mdash;see [limitations](#limitations).

Patterns can be nested:

```d
auto (a, (b, c)) = tuple(1, tuple("2", 3.0));
(int a, (string b, double c)) = tuple(1, tuple("2", 3.0));
```

When a pattern component declares a variable name, it must have either a type specified, or at least one storage class applying to it:

```d
(int a, b) = tuple(1, "2"); // error
(int a, auto b) = tuple(1, "2"); // ok

(int a, (b, c)) = tuple(1, tuple("2", 3.0)); // error
(int a, auto (b, c)) = tuple(1, tuple("2", 3.0)); // ok
(int a, (auto b, auto c)) = tuple(1, tuple("2", 3.0)); // ok
```

Note: This rule leaves open the possibility of supporting unpacking tuple components into
a new variable declaration and [an existing lvalue expression](#postponed-unpacking-assignments)
within the same unpack declaration (as in C#).

`static` or `enum` can be applied to the whole declaration, but not to individual components.

### Unpacking `foreach` elements

Similarly to unpacking variable declarations, `foreach` should also support unpack declarations.
For consistency with existing `foreach` variable declarations, the `auto` storage class is
implied unless overridden. The following `foreach` statements are equivalent:

```d
auto arr = [tuple(1, "2"), tuple(3, "4"), tuple(5, "6")];

foreach((x, y); arr) {
    writeln(x, " ", y); // "1 2\n3 4\n5 6"
}

foreach((int x, string y); arr) {
    writeln(x, " ", y);// "1 2\n3 4\n5 6"
}
```

An index variable can be declared alongside
an unpacked element variable (when the *ForeachAggregate* supports it):

```d
import std.typecons : t = tuple;

auto arr = [t(1, 2), t(3, 4)];

// declare index for array & unpack tuple elements
foreach(i, (x, y); arr) {
    assert(x==2*i+1 && y==2*i+2);
}

import std.range;

// unpack a range of nested tuples
foreach(i, (j, k); enumerate(arr)) {
    writeln(i," ",j," ",k); // "0 1 2\n1 3 4\n"
}
```

The index variable can also be unpacked:

```d
auto aa = [t(1, 2): "hi", t(3, 4): "bye"];
foreach ((a, b), s; aa)
    writeln(a, b, s); // "12hi\n34bye\n"
```

Normal `foreach` storage classes are supported, and they can apply to tuple component
variables:

```d
auto arr = [t(1, 2), t(3, 4)];
foreach((ref x, y); arr) {
    x = 2*y;
}
assert(arr == [t(4, 2), t(8, 4)]);

foreach(const (x, y); arr) {
    static assert(is(typeof(x) == const(int)));
    static assert(is(typeof(y) == const(int)));
    assert(x == 2*y);
}
```

Unpacking can be used with `static foreach`:

```d
static foreach((a, b); [t(1,2), t(3,4)])
    pragma(msg, a, b); // "12\n34\n"
```

Unpacking can also be used with `opApply` and even when a range
[has an element type which is a value sequence](https://dlang.org/spec/statement.html#front-seq),
assuming one of the sequence components is itself a sequence. See [examples](#examples).


### Postponed: Unpacking assignments

Unpacking a tuple into lvalues is postponed until tuple literals are supported, because both
redefine the comma operator syntax.

```d
auto t = (1, 2); // tuple literal
int x, y;
(x, y) = t;
assert(x == 1);
assert(y == 2);
```

Note: Using the result of a comma expression is already an error.

### Unpacking function literal parameters

A function parameter must have a type, so function parameters cannot be unpacked without
tuple types, which are beyond the scope of this DIP. However, a function literal template
with an untyped unpack declaration parameter is supported:

```d
alias dg = ((x,y), z) => writeln(x," ",y," ",z);
dg(tuple(1, 2), 3); // "1 2 3\n"
```

`((x,y), z){ ... }` is lowered to `(__arg0, z){ auto (x,y) = __arg0; ...}`. In general, it preserves and copies storage classes from
the parameter to the unpack declaration in the function body.

`ref` storage class is copied outwards, e.g., `((ref a,b), c){ ... }` gets lowered to
`(ref __arg0, c){ (ref a,auto b) = __arg0; ... }`. 

`out` can be applied to the whole unpack parameter, but not to individual fields. `auto ref` and `lazy` cannot
be applied to an unpacking parameter list.

Note: `out` unpacking parameters are not supported by the current implementation, but may be added at a future date.

### Grammar changes

The following grammar additions will be sufficient.
(Note that many grammar rules are repeated due to (existing) distinct restrictions on which `StorageClasses` are allowed; this can make the proposed grammar changes appear more profound than they are.)

#### Unpacking declarations

See <https://dlang.org/spec/declaration.html#VarDeclarations>.

```diff
  VarDeclarations:
      ...
      AutoDeclaration
+     StorageClasses[opt] TupleDeclarators ;

+ TupleDeclarators:
+     TupleDeclarator
+     TupleDeclarator , TupleDeclarators

+ TupleDeclarator:
+     ( TupleDeclarators2 ) = Initializer

+ TupleDeclarators2:
+     TupleDeclarator2 ,
+     TupleDeclarator2 , TupleDeclarator2
+     TupleDeclarator2 , TupleDeclarators2

+ TupleDeclarator2:
+     StorageClasses[opt] Identifier
+     StorageClasses[opt] BasicType TypeSuffixes[opt] Identifier
+     StorageClasses[opt] ( TupleDeclarators2 )
```

#### Unpacking `foreach` elements

(Note that `ForeachTupleDeclarators2` is the same as `TupleDeclarators2`, except that the latter allows all `StorageClasses`, while the former allows only `ForeachTypeAttributes`.)

See <https://dlang.org/spec/statement.html#ForeachType>.

```diff
  ForeachType:
      ...
      ForeachTypeAttributes[opt] alias Identifier
+     ForeachTypeAttributes[opt] ( ForeachTupleDeclarators2 )

+ ForeachTupleDeclarators2:
+     ForeachTupleDeclarator2 ,
+     ForeachTupleDeclarator2 , ForeachTupleDeclarator2
+     ForeachTupleDeclarator2 , ForeachTupleDeclarators2

+ ForeachTupleDeclarator2:
+     ForeachTypeAttributes[opt] Identifier
+     ForeachTypeAttributes[opt] BasicType TypeSuffixes[opt] Identifier
+     ForeachTypeAttributes[opt] ( ForeachTupleDeclarators2 )
```

#### Unpacking function literal parameters

(Note that `ParameterTupleDeclarator2` is the same as `TupleDeclarator2`, except that the latter allows all `StorageClasses`, while the former allows only `ParameterAttributes`.)

See <https://dlang.org/spec/function.html#Parameter>.

```diff
Parameter:
      ...
      ParameterDeclaration = AssignExpression ...
+     ParameterAttributes[opt] ParameterTupleDeclarator

+ ParameterTupleDeclarator:
+     ( ParameterTupleDeclarators2 )
+     ( ParameterTupleDeclarators2 ) = Initializer

+ ParameterTupleDeclarators2:
+     ParameterTupleDeclarator2 ,
+     ParameterTupleDeclarator2 , ParameterTupleDeclarator2
+     ParameterTupleDeclarator2 , ParameterTupleDeclarators2

+ ParameterTupleDeclarator2:
+     ParameterAttributes[opt] Identifier
+     ParameterAttributes[opt] BasicType TypeSuffixes[opt] Identifier
+     ParameterAttributes[opt] ( ParameterTupleDeclarators2 )
```

### Examples

```d
import std.stdio : writeln;
import std.typecons : t = tuple, T = Tuple;

unittest{
     // unpack declarations
     auto (a, (b, c)) = t(1, t(2, "3"));
     assert(t(a, b, c) == t(1, 2, "3"));

     import std.string, std.conv;
     auto (u, v) = "4 5".split.to!(T!(int,int));
     assert(u == 4);
     assert(v == 5);
}

unittest{
     // works with opApply
     static struct Iota2d{
         int start,end;
         int opApply(scope int delegate(T!(int,int)) dg){
             foreach(i; start .. end) {
                 foreach(j; start ..end) {
                     if(auto r = dg(t(i,j)))
                         return r;
                 }
             }
             return 0;
         }
     }
     bool[4][4] visited;
     foreach((x, y); Iota2d(0,4)){
         visited[x][y] = true;
     }
     import std.algorithm;
     assert(visited[].all!((ref x)=>x[].all));
}

unittest{
     // works with ranges of tuples
     struct TupleRange
     {
         size_t i = 1;
         auto front() => t(i, i + 1);
         bool empty() => i == 5;
         void popFront() { i += 2; }
     }
     import std.range;
     foreach (i, (a, b); enumerate(TupleRange())) {
         writeln(i, a, b); // "012\n134\n"
     }
}

unittest{
     import std.algorithm;
     // can unpack in lambda parameter list
     [t(1,2),t(2,3)].map!( ((a, b)) => a+b ).each!writeln; // "3\n5\n"

     // works with storage classes
     auto arr = [t(1, 2), t(3, 4)];
     arr.each!( ((ref x, y)){ x = 3*y; });
     assert(arr.all!( (const (x, y)) => x == 3*y));
}
```

Example from DIP 32, originally by bearophile:

```d
import std.stdio, std.algorithm, std.container, std.array;
import std.typecons : t = tuple;

auto encode(T)(Group!("a == b", T[]) sf) {
    auto heap = sf.map!( ((c, f)) => t(f, [t(c, "")]) ).array.heapify!q{b < a};

    while (heap.length > 1) {
        auto (lof, loa) = heap.front;  heap.removeFront;
        auto (hif, hia) = heap.front;  heap.removeFront;
        foreach ((_, ref e); loa) e = '0' ~ e;
        foreach ((_, ref e); hia) e = '1' ~ e;
        heap.insert(t(lof + hif, loa ~ hia));
    }
    return heap.front[1].schwartzSort!( ((c, e)) => t(e.length, c) );
}

void main() {
    auto s = "this is an example for huffman encoding"d;
    foreach ((c, e); s.dup.sort().release.group.encode)
        writefln("'%s'  %s", c, e);
}
```


### Limitations

* Moving elements (rather than copying) from the value sequence is not supported.
* Unpacking from a container with a statically known length is not supported unless
  it implicitly converts to a value sequence. Better support for this could be added in
  the future. Note that a static array `sa` can be unpacked using
  [`sa.tupleof`](https://dlang.org/spec/arrays.html#array-properties).
* Unpacking fields from an aggregate type instance is not supported, though `.tupleof`
  can again be used to obtain an lvalue sequence.
* Unpacking in function parameter lists (that are not function literals) is not supported,
  but should be if built-in tuple types are added.
* Declaring a variable that is a type sequence instance is not supported by the current
  implementation, but should work:
  ```d
  import std.meta, std.typecons;
  auto (AliasSeq!(int, int) x, y) = tuple(1, 2, 3);
  ```
* Unpacking zero elements should be supported for generic programming:
  ```d
  auto () = tuple();
  auto (a, ()) = tuple(1, tuple());
  ```
  The current implementation does not support this, due to
  <https://github.com/dlang/dmd/issues/20842>.
* The number of elements has to match exactly. This restriction may be relaxed in a future DIP.
  For now, slicing the value sequence can remove elements at the front or back before unpacking.

## Breaking Changes and Deprecations

This DIP introduces completely new syntax and semantics. As such, it cannot break existing code. In particular, the following will continue to have the same semantics:

* any expression statement beginning with a function literal;
* any [ScopeGuardStatement](https://dlang.org/spec/statement.html#scope-guard-statement).

These can be distinguished in parsing by testing if an `=` token immediately follows the closing parenthesis which matches the first opening parenthesis after the storage class.

## Reference

- Walter's original post on tuple unpacking (2010): http://forum.dlang.org/post/i8jo6k$sba$1@digitalmars.com  
  More posts on the topic can be found around October 07, 2010 (threading problems): https://forum.dlang.org/group/general?page=1067
- DIP 32 (listing many alternative proposals): https://wiki.dlang.org/DIP32
- Some of the feature requests on the forums:
  - (2013) http://forum.dlang.org/post/gridjorxqlpoytuxwpsg@forum.dlang.org
  - (2017) https://forum.dlang.org/thread/glpsggkvxsiwxwfolwog@forum.dlang.org
    - Includes discussion with Steven Schveighoffer, which inspired proposal 2
  - (2017) http://forum.dlang.org/post/cktzfujipzzlloacthka@forum.dlang.org
- Killing the comma operator (2016): https://forum.dlang.org/thread/vcuinavnczqssdyewbjr@forum.dlang.org
- Timon's tuple unpacking syntax thread:
  https://forum.dlang.org/post/vbda5s$fng$1@digitalmars.com

## Copyright & License

Copyright (c) 2025 by the D Language Foundation.

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## History

This DIP [was discussed in the DIP Ideas forum](https://forum.dlang.org/post/mjzhwbwuvnfqimwazzxq@forum.dlang.org).

This DIP underwent [one round of discussion in the DIP Development forum](https://forum.dlang.org/post/dlzlsmaaftmqifcadisp@forum.dlang.org).

The language maintainers approved this DIP with the following rationale:

Tuples are a significant feature, and experience with other languages shows it has utility. D already has tuples, but they are clumsy to use. This DIP is good, sensible progress in making tuples easier and more natural.

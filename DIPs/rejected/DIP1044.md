# Enum Type Inference

| Field           | Value                                                           |
|-----------------|-----------------------------------------------------------------|
| DIP:            | 1044                                                            |
| Review Count:   | 2                                                               |
| Author:         | Aya Partridge (zxinsworld@gmail.com)                            |
| Implementation: | <https://github.com/dlang/dmd/pull/14650>                       |
| Status:         | Rejected                                                        |

## Abstract
Enum Type Inference (henceforth ETI) is a shortcut to allow the omission of an enum member's
type name where it can unambiguously be contextually inferred. Its adoption by other popular
languages has thoroughly proven its usability and efficacy of reducing unnecessary type repitition.

## Contents
* [Rationale](#rationale)
* [Prior Work](#prior-work)
* [Description](#description)
* [Concessions](#concessions)
* [Rejected Alternatives](#rejected-alternatives)
* [Grammatical Changes](#grammatical-changes)
* [Reference](#reference)
* [Copyright & License](#copyright--license)
* [Reviews](#reviews)

## Rationale
When using a member of an enum type in D, one is required to prefix the member name with the enum's type identifier, e.g., `MyEnum.enumMember`.
Even when the compiler could unabiguously determine an enum member's type automatically,
it is still required for the programmer to write the enum's type identifier each and every time.
Writing enum type names repeatedly in cases where they could be inferred by the compiler breaks the [DRY principle](https://en.wikipedia.org/wiki/Don%27t_repeat_yourself).

The popular solution offered by several other modern languages is simple: much like `auto`,
we simply permit the omission of an enum member's type identifier when the
type can be unambiguously inferred from the enum member's context.
Having an equivalent shortcut in D would reduce repitition, while still providing adequate readability.
When ETI is used, the programmer can recognize that they are looking at an enum member whose type has been omitted and knows to look for it elsewhere, e.g., a function's declaration, if it is not already apparent. This is distinct from C and C++, in which enum members appear identical to variable names.

## Prior Work
Implementation of this feature in other languages:
- [Swift](https://docs.swift.org/swift-book/LanguageGuide/Enumerations.html)
- [The V Programming Language](https://github.com/vlang/v/blob/master/doc/docs.md#enums)
- [Ziglang](https://ziglang.org/documentation/master/#Enum-Literals)
- [Odin](https://odin-lang.org/docs/overview/#implicit-selector-expression)

Java allows omitting the enum type, but only in switch-case statements: https://docs.oracle.com/javase/tutorial/java/javaOO/enum.html

A suggestion for a similar feature in Rust but with a different approach: https://internals.rust-lang.org/t/enum-path-inference-with-variant-syntax/16851

## Description
The design goals of this proposal are for ETI to be as easy to type and
as intuitive to read and understand as possible for new and existing users, while not breaking existing D code.
While currently limited to enums, ETI's syntax has been designed so that the feature may eventually be generalised to other parts of the language in the future if it is found to be desirable.

This DIP proposes that ETI in D should use the syntax `$enumMember`. The proposed syntax has
been chosen to represent the best possible compromise between each of ETI's core design goals.
See the [Concessions](#concessions) and [Rejected Alternatives](#rejected-alternatives) sections for more information.

The following is an exhaustive list of circumstances in which ETI will be permitted.

### 1. Initialisers and Assignments
ETI is allowed when initialising or assigning to a variable that has a known enum type.
```d
enum A{ a,b,c,d }

struct S{ A one, two; }

void main(){
    A    myA1 = $b;      // myA1 = A.b
    A    myA2 = $b | $c; // myA2 = A.c
         myA2 += $b;     // myA2 = A.d
         myA2 = $d - 1;  // error, cannot determine type of "$d" because expression returns int!
    auto myA3 = $c;      // error, we don't know the type of "$c"!
    A    myA4 = $a + 1;  // error, assign an int to variable type A!

    S myS;
    myS.one = $c; // myB.one = A.c
    myS.two = $d; // myB.two = A.d
}
```

### 2. Return Statements
ETI is allowed in return statements when a function has an explicit enum return type.
```d
enum A{ a,b,c,d }

A myFn(){
    return $c; //returns A.c
}

auto myBrokenFn(){
    return $c; // error, we don't know the type of "$c"!
}
```

### 3. Argument Lists
ETI is allowed in the argument lists of function calls and template instantiations when they can bind to explicitly typed enum parameters.
```d
enum A{ a,b,c,d }

struct S{ A one, two; }

void myFn(A param){}
void myDefaultFn(A param=$d){}
void myTempFn(T)(T param){}

void main(){
    S    myS1 = {one: $a, two: $b};
    auto myS2 = S($a, $b);

    myFn($a);
    myFn($b + $b); // passes A.c

    myDefaultFn();
    myDefaultFn($c);

    myTempFn!A($a);
    myTempFn($a);       // error, can't infer a type to instantiate the template with from "$a"!
    myFn(myTempFn($a)); // error, same as above
}
```

### 4. Case Statements
ETI is allowed in `case` statements that match the type of the `switch` where the `switch` type is an enum.
```d
import std.stdio: writeln;

enum A{ a,aa,aaa }
enum WordLetterOfTheDay{ a,b,c,d/*...*/ }

void main(){
    auto letterToday = WordLetterOfTheDay.b;

    switch(letterToday){
        case $a:
            writeln("Apple");
            break;
        case $b:
            writeln("Bicycle");
            break;
        case $c:
            writeln("Caterpillar");
            break;
        case $d:
            writeln("Didgeridoo");
            break;
        /*...*/
        case $aa: // error, "aa" is not a member of WordLetterOfTheDay!
            break;
        default:
    }
}
```

### 5. Array Literals & Associative Array Literals
A) ETI is allowed in array and associative array literals in initializations and assignments as determined by [Initialisers and Assignments](#1-initialisers-and-assignments) above.

B) ETI is also allowed in array and associative array literals for which an explicit enum type can be inferred.
```d
enum A{ a,b,c,d }

// (A)
A[4] myArr1 = [$a, $b, $c, $d];
// (B)
auto myArr2 = [A.a, $b, $c, $d]; // typeof(y) = A[]
auto myArr3 = [A.c, 64, $b, $b]; // error, ETI can't be performed because this array will be an int[]!

// (A)
A[][] myNestArr1 = [[$a,  $b], [$c,  $d]]; // myNestArr1 = [[A.a, A.b], [A.c, A.d]]
// (B)
auto  myNestArr2 = [[A.a, $b], [A.c, $d]]; // myNestArr2 = [[A.a, A.b], [A.c, A.d]]
auto  myNestArr3 = [[A.a, $b], [$c,  $d]]; // error, array "[$c, $d]" is evaluated separately, so its type cannot be determined!

// (A)
int[A] myMap1 = [$a: 20, $b: 21, $c: -3, $d: 45];
A[int] myMap2 = [0: $a, 1: $b, 2: $c, 3: $d];
// (B)
auto myMap3 = [A.a: 31, $b: 463, $c: -13, $d: 37]; // typeof(y) = int[A]
auto myMap4 = [0: A.a, 1: $b, 2: $c, 3: $d]; // typeof(y) = A[int]
```

### 6. Indexing
ETI is allowed when indexing a variable that is indexed by an enum of the same type.
```d
enum A{ a,b,c,d }
enum B{ e,f,g,h }

struct S{
    int opIndex(A param) => cast(int)param;
}

void main(){
    int[A] myMap = [$a: 1, $b: 24, $c: -13, $d: 37];
    myMap[$b] += 1;
    auto x = myMap[$b]; // x = 25

    S myS;
    auto y = myS[$d]; // y = 3
    y = myS[$e]; // error, we don't know the type of "$e" because it's not a member of the type of the first argument of "S.opIndex(A param)"!

    A[] myArr;
    auto z = myArr[$c]; // error, we don't know the type of "$c"!
}
```

### Other considerations
When the resolution of ETI is ambiguous due to multiple candidate enum types, ETI is prohibited.
```d
enum A{ a,b,c,d,e }
enum B{ a,b,c,d }

void myFn(A param){}
void myFn(B param){}

void main(){
    myFn(A.a);
    myFn($a); // error, we have two equally valid candidates!
    myFn($e); // OK, the only candidate is "myFn(A.e)", since B does not have "$e"
}
```

ETI also cannot circumvent visibility attributes.
```d
module x;

private enum PrivEnum{ a,b,c,d }
auto myFn(PrivEnum x){ return x; }
```
```d
import x;

auto a = x.myFn(PrivEnum.a); //error, PrivEnum is private!
auto b = x.myFn($a);         //error, same as above
```

## Concessions
Many concessions have been made regarding ETI's syntax in order to uphold the DIP's goals. Here is a brief list.

### Allowing ETI in `int[]` array literals and in `int` to `enum` assignment
Formerly, this DIP proposed the following syntax.
```d
enum A{ a,b,c,d }

auto  myArr1 = [A.c, $b, 64, $a]; // typeof(z) = int[]
int[] myArr2 = [A.a, $a];
```
`int[]`-inferred array literals that use only one type of enum in their elements could have still used ETI.
This was scrapped due to its poorly-defined and unintuitive nature, which would've also made it difficult to implement.

Similarly, the following syntax was also briefly considered before being dropped for similar reasons.
```d
enum A{ a,b,c,d }

void main(){
    A myA1;
      myA1 = $d - 1; // myA1 = A.c
    A myA2 = $a + 1; // myA2 = A.b
}
```

### Using `.member` or `..member` instead of `$member`
While it could have been implemented without breaking existing code, the `.member` syntax was scrapped due to requiring an unintuitive and performance-degrading interaction with the existing [module scope operator](https://dlang.org/spec/module.html#module_scope_operators)
(`.x` would have to prioritise module-level variables over enum members), which would have been difficult to implement and may have limited the feature's future potential to be generalised.

After being trialled, `..member` was deemed to blend in too well with the range operator and
other existing uses of dots in D.

## Rejected Alternatives
The D Language community has brought up many possible alternatives to this DIP's
proposed solution to enum type repitition. The following are some noteworthy mentions.

### Using `with` statements instead
For some use cases, an argument could be made against the need for ETI. For instance, a `final switch` statement combined with a `with` statement:
```d
enum A{ a, b, c }

void main(){
    auto myA = A.b;
    final switch(myA) with(typeof(myA)){
        case a:
            // ...
            break;
        case b:
            // ...
            break;
        case c:
            // ...
            break;
    }
}
```
In the above case, ETI is not strictly necessary.

However, there are many everyday cases where using the `with` statement would prove insufficient.
For example, given the following declarations:
```d
enum Size{ small, medium, large }
enum Shape{ square, round, triangular }
enum State{ on, off, undefined }

struct Obj{
    Size  size;
    Shape shape;
    State state;

    void myMeth(Size size, Shape shape, State state){}
}
```
Let's write some code that doesn't yet use ETI or `with` statements:
```d
void myFn1(){
    Obj myObj = {
        size:  Size.large,
        shape: Shape.round,
        state: State.on,
    };
    myFn2(myObj);
}

void myFn2(ref Obj myObj){
    myObj.myMeth(Size.medium, Shape.square, State.undefined);
}
```
Next, let's rewrite it, but using ETI:
```d
void myFn1(){
    Obj myObj = {
        size:  $large,
        shape: $round,
        state: $on,
    };
    myFn2(myObj);
}

void myFn2(ref Obj myObj){
    myObj.myMeth($medium, $square, $undefined);
}
```
And finally, we'll rewrite it to use `with`:
```d
void myFn1(){
    with(Size) with(Shape) with(State){
        Obj myObj = {
            size:  large,
            shape: round,
            state: on,
        };
    }
    myFn2(myObj);
}

void myFn2(ref Obj myObj){
    with(Size) with(Shape) with(State){
        myObj.myMeth(medium, square, undefined);
    }
}
```
The version using ETI requires less typing than the version without it and loses little-to-no readability.
The same can't be said for the `with` version, which produces a less readable result becuse
it requires the programmer to keep track of which types are in the nested `with` scopes. Otherwise `large`, `round`, etc. might be confused for regular variable names.
Additionally, the example using `with` statements requires even more typing than the original.
When the use of many enum types is spread out across a code base like this,
`with` is rendered ineffective for reducing enum type repitition.

### Adding implicit `with` statements
Many (including Walter Bright) have proposed that in situations where an enum is to be supplied,
a `with` statement over its type is implicitly used.
```d
enum A{ a,b,c,d }

struct S{ A one, two; }

void main(){
    A    myA1 = b;      // myA1 = A.b
    A    myA2 = b | c;  // myA2 = A.c
    auto myA3 = b;      // error, b is undefined

    S myS;
    myS.one = c; // myB.one = A.c
    myS.two = d; // myB.two = A.d
}
```
This idea was rejected because it contradicts the goals of this proposal by being harder to
comprehend and read (much like enums in C and C++), introducing many breaking changes and
limiting the future potential for type inference to be generalised to other parts of the language.

### Using `:member` instead of `$member`
While fairly popular, this syntax can be difficult to distinguish from surrounding code in many everyday contexts:
```d
enum A{ a,b,c,d }

void main(){
    switch(z){
        case :a: // looks like :a:
            break;
        case :b : // an attempt to be more readable, but could easily be mistaken for "case (nothing):" and then "b:"
            break:
        default:
    }

    A[int] myMap1 = [:a:1, :b:2, :c:3]; // it's our friend :a: again!
    int[A] myMap2 = [1::a, 2::b, 3::c]; // oh no, it's C++!

    A x = :a; // looks like :a: at a glance
    A y = x ? : b : : d; // the use of whitespace which ":" is part of the ternary operator is unclear without extra scrutiny
    A z = x ? z ? :c : :d : :a; // uh oh
}
```
While `$member` can be nearly as confusing when used for indexing alongside `opDolar` (e.g. `myObj[$ + $x]`),
this is an incredibly niche use-case for enums. Especially when compared to ternary operators, `case` statements,
initialisers with named fields, and (in the future) named function arguments.

## Grammatical Changes
```diff
PrimaryExpression:
+   $ Identifier
```

## Reference
- [DIPX: Enum Literals / Implicit Selector Expression](https://forum.dlang.org/thread/yxxhemcpfkdwewvzulxf@forum.dlang.org)
- [Enum literals, good? bad? what do you think?](https://forum.dlang.org/thread/zvhelliyehokebybmttz@forum.dlang.org)
- [Implementing Parent Enum Inference in the language (.MyValue instead of MyEnum.MyValue) #88](https://github.com/dlang/projects/issues/88)

## Copyright & License
Copyright Â© 2022 by the D Language Foundation

Licensed under [Creative Commons Zero 1.0](https://creativecommons.org/publicdomain/zero/1.0/legalcode.txt)

## Reviews
### Community Review Round 1
[Reviewed Version](https://github.com/dlang/DIPs/blob/e2ca557ab9d3e60305a37da0d5b58299e0a9de0e/DIPs/DIP1044.md)

[Discussion](https://forum.dlang.org/thread/wpqmuysuxadcwnzypnxk@forum.dlang.org)

[Feedback](https://forum.dlang.org/thread/yhlkkahzhabrhtyyqtpd@forum.dlang.org)

The following items of actionable feedback were provided in the Feedback Thread.

* The second paragraph of 'Description' is too vague. A DIP's description should be complete and unambiguous. The DIP author suggested it could be revised with help from some experienced implementers.
* The DIP does not address what happens in combination with function and template overloading. The DIP author said this is addressed in 'Other considerations'.
* The DIP does not address what happens in the case of `auto x = e` when a variable `int e` and `enum E {e}` are in module scope. The DIP author noted the example should be `auto x = $e`, which would always resolve to `E.e`.
* The DIP does not address scoping rules, e.g., what happens in `auto x = e` when local variable `int e` shadows `enum E {e}` in module scope. The DIP author noted the example should be `auto x = $e`, which would always resolve to `E.e`.
* The C-style of enums should be mentioned in 'Prior Work'. The DIP author disagrees, as C-style emums have different behavior.
* A special syntax for declaring enums that use ETI, e.g., `enum NewKeywordHere { ex = 1 }` would be less disruptive than adding `$` and new lookup rules. The DIP author disagreed, arguing this might cause old code to break, would make it more difficult to distinguish between enums with inferred types and variables with the same names, and would cause confusing name conflicts.
* The `$e` syntax will require a search of every enum in scope, which can be slow. A separate symbol table will likely need to be maintained. The DIP author described an approach to resolution and asked if it was feasible.
* The rule about array inference is unclear. Is the rule set by the type of the first element? Now, the type of an array literal is determined by the common type of all elements, not the first element. The author responded that the initial draft specified the first element, but the DIP manager suggested removing it because "D already infers the type of array literals when it can", then asked if the first element rule be added in again.
* What about nested arrays? The DIP author says the behavior should be consistent with existing behavior.
* Consider an alternative to the acronym "ETI", which in C++ means "explicit template instantiation". The DIP author responded by saying that D is not C++, but suggested that the feature should go into the spec as "type inference".
* Using new symbols for a minor feature is wasteful. "implicit with" would be a more elegant approach. The DIP author said that it's a minor feature now, but could be more generalized later. She also suggested if "implicit with" is only used in case statements, that defeats the purpose of this proposal.
* The 'Rationale' section is mostly empty, expressing a personal opinion that qualifying enums "can be tedious". It should be expanded to clarify the problem the DIP intends to solve. The DIP author agreed and commited to expanding that section.

### Formal Assessment
The language maintainers chose to reject this proposal. They said it was well done and an interesting read, but in the end found it to be too complex for insufficient benefit.

They listed several concerns that led to their decision:

* Given that D already has `with`, `alias`, and `auto`, it does not seem worthwhile to add a special case for enums.
* Semantic analysis in D is strictly bottom-up. This proposal would add top-up type inference on top of that. This presents problems in handling function and template overloads, as well as variadic parameter lists.
* The proposal only allows ETI in some contexts. This is potentially confusing for the programmer, particularly in the presence of mixin templates (which draw symbols from the instantiation context) and function overloads.
* Symbol tables can get very large. Lookups are done via hash table for max speed, but this will not work for ETI. There may be a very large number of "enums in scope", and each one will have to be searched to resolve a member.
* ETI is similar to ADL (Argument Dependent Lookup) in C++. Walter Bright implemented ADL in the Digital Mars C++ compiler and is strongly against allowing anything like it in D. He finds it slow and complex, and few people really know how it's going to work.
